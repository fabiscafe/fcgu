# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.5.1+r57+g8d2d156
pkgrel=1
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/World/podcasts"
license=(GPL3)
options=(debug)
depends=(gst-plugins-bad gst-plugins-base gst-plugins-good libadwaita)
makedepends=(appstream-glib git meson rust)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
conflicts=(gnome-podcasts-gtk4)
provides=(gnome-podcasts-gtk4)
replaces=(gnome-podcasts-gtk4)
_commit=8d2d156fb98a7d4aba7f7a89865f4c42568ca87d # master^0
source=("git+${url}.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd podcasts
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd podcasts
}

build() {
  arch-meson podcasts build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
