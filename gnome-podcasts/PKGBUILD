# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.5.1+r99+g922b9d3d
pkgrel=1
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/World/podcasts"
license=(GPL3)
depends=(
  cairo
  dbus
  dconf
  gdk-pixbuf2
  glib2
  gst-plugins-bad
  gst-plugins-bad-libs
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  openssl
  pango
  sqlite
)
makedepends=(
  appstream-glib
  git
  meson
  rust
)
optdepends=(
  'gst-plugins-ugly: Extra media codecs'
  'gst-libav: Extra media codecs'
)
_commit=922b9d3d8ca207007e23c93acff0c5b49d3c109c # master^0
source=("git+https://gitlab.gnome.org/World/podcasts.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd podcasts
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd podcasts
}

build() {
  arch-meson podcasts build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
