# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.5.1+r61+gf8d30dd
pkgrel=1
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/World/podcasts"
license=(GPL3)
depends=(
  gst-plugins-bad
  gst-plugins-base
  gst-plugins-good
  libadwaita
)
makedepends=(
  appstream-glib
  git
  meson
  rust
)
optdepends=(
  'gst-plugins-ugly: Extra media codecs'
  'gst-libav: Extra media codecs'
)
_commit=f8d30dd75f8125d5d37ceb3676953c6cc8ebab9b # master^0
source=("git+https://gitlab.gnome.org/World/podcasts.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd podcasts
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd podcasts
}

build() {
  arch-meson podcasts build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
