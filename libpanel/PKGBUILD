# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libpanel
pkgname=(libpanel libpanel-docs)
pkgver=1.1.0
pkgrel=1
pkgdesc="Dock/panel library for GTK 4"
url="https://gitlab.gnome.org/GNOME/libpanel"
arch=(x86_64)
license=(LGPL)
depends=(gtk4 libadwaita)
makedepends=(git meson vala gobject-introspection gi-docgen)
options=(debug)
_commit=1dc0ffc6646781a69d1f0b4651d0d5513ff51d83  # tags/1.1.0^0
source=("git+https://gitlab.gnome.org/GNOME/libpanel.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  local meson_options=(
    -D install-examples=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libpanel() {
  provides=(libpanel-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libpanel-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
