# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=libpanel
pkgver=1.0.1
pkgrel=0.1
pkgdesc="helps to create IDE-like applications using GTK 4 and libadwaita."
url="https://gitlab.gnome.org/chergert/libpanel"
arch=(x86_64)
license=(LGPL)
depends=(libadwaita)
makedepends=(git gi-docgen gobject-introspection meson vala)
provides=(libpanel-1.so)
options=(debug)
_commit=1403a020e5d65e21bc0e16133aeb2b6e7cdca2de  # tags/1.0.1^0
source=("git+https://gitlab.gnome.org/chergert/libpanel.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
