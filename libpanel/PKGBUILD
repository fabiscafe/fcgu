# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=libpanel
pkgver=1.0alpha
pkgrel=1
pkgdesc="helps to create IDE-like applications using GTK 4 and libadwaita."
url="https://gitlab.gnome.org/chergert/libpanel"
arch=(x86_64)
license=(LGPL)
depends=(libadwaita)
makedepends=(git gobject-introspection meson vala)
provides=(libpanel-1.so)
options=(debug)
_commit=ecd5c578cbe8b4468b5ec00128c13c8c1570820b  # tags/1.0.alpha^0
source=("git+https://gitlab.gnome.org/chergert/libpanel.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:

