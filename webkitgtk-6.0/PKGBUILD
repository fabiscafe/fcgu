# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

# Name change due to API change of former webkit2gtk-5.0 to webkitgtk-6.0. This also reflects the library names.
# Other Projects
# https://gitlab.gnome.org/GNOME/gnome-build-meta/-/merge_requests/1864
# https://src.fedoraproject.org/rpms/webkitgtk/c/f1d95b90e57fec87a68637fb6270dd7ab4dbd50f?branch=rawhide
pkgbase=webkitgtk-6.0
pkgname=(
  webkitgtk-6.0
  webkitgtk-6.0-docs
)
pkgver=2.39.91
pkgrel=1
pkgdesc="Web content engine for GTK"
url="https://webkitgtk.org"
arch=(x86_64)
license=(custom)
depends=(
  at-spi2-core
  atk
  bubblewrap
  cairo
  enchant
  fontconfig
  freetype2
  glib2
  gst-plugins-base-libs
  gstreamer
  gtk4
  harfbuzz
  harfbuzz-icu
  hyphen
  icu
  libavif
  libegl
  libgcrypt
  libgl
  libgles
  libice
  libjpeg
  libjxl
  libmanette
  libnotify
  libpng
  libseccomp
  libsecret
  libsoup3
  libsystemd
  libtasn1
  libwebp
  libwpe
  libx11
  libxcomposite
  libxext
  libxml2
  libxslt
  libxt
  openjpeg2
  sqlite
  wayland
  woff2
  wpebackend-fdo
  xdg-dbus-proxy
  zlib
)
makedepends=(
  cmake
  gi-docgen
  gobject-introspection
  gperf
  gst-plugins-bad
  ninja
  python
  ruby
  systemd
  unifdef
  wayland-protocols
)

# !lto: https://bugs.webkit.org/show_bug.cgi?id=249368
options=(!lto)
source=(
  $url/releases/webkitgtk-$pkgver.tar.xz{,.asc}

  # [GLib] Clean builds fail with ENABLE_2022_GLIB_API #1
  ## https://github.com/WebKit/WebKit/pull/11221
  0001-GLib_Clean_builds_fail_with_ENABLE_2022_GLIB_API.patch::https://patch-diff.githubusercontent.com/raw/WebKit/WebKit/pull/11221.patch

  # [GLib] Clean builds fail with ENABLE_2022_GLIB_API #2
  ## https://github.com/WebKit/WebKit/pull/11222
  0002-GLib_Clean_builds_fail_with_ENABLE_2022_GLIB_API.patch::https://patch-diff.githubusercontent.com/raw/WebKit/WebKit/pull/11222.patch
)
sha256sums=('92e8a6f599450529eb0bd552e4fc2d1f691831eb4d89caffb96fb581d2c836d3'
            'SKIP'
            '687806df3fc62238374d6ba171cdb16802f92a6739d6bf2c42bedac8514f593d'
            '2c3bedb4956861764f34b1864187871d7702704c32dcf696d65894839b82c941')
validpgpkeys=('D7FCF61CF9A2DEAB31D81BD3F3D322D0EC4582C3'  # Carlos Garcia Campos <cgarcia@igalia.com>
              '5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

prepare() {
  cd webkitgtk-$pkgver

  patch -Np1 -i "$srcdir/0001-GLib_Clean_builds_fail_with_ENABLE_2022_GLIB_API.patch"
  patch -Np1 -i "$srcdir/0002-GLib_Clean_builds_fail_with_ENABLE_2022_GLIB_API.patch"
}

build() {
  local cmake_options=(
    -DPORT=GTK
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DCMAKE_INSTALL_LIBEXECDIR=lib
    -DCMAKE_SKIP_RPATH=ON
    -DUSE_AVIF=ON
    -DUSE_GTK4=ON
    -DUSE_JPEGXL=ON
    -DENABLE_DOCUMENTATION=ON
    -DENABLE_MINIBROWSER=ON
  )

  # Produce minimal debug info: 4.3 GB of debug data makes the
  # build too slow and is too much to package for debuginfod
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  cmake -S webkitgtk-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package_webkitgtk-6.0() {
  depends+=(
    libwpe-1.0.so
    libWPEBackend-fdo-1.0.so
  )
  provides+=(
    libjavascriptcoregtk-6.0.so
    libwebkitgtk-6.0.so
  )
  conflicts=(webkit2gtk-5.0)
  replaces=(webkit2gtk-5.0)
  optdepends=(
    'geoclue: Geolocation support'
    'gst-plugins-good: media decoding'
    'gst-plugins-bad: media decoding'
    'gst-libav: nonfree media decoding'
  )

  DESTDIR="$pkgdir" cmake --install build

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc

  cd webkitgtk-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_webkitgtk-6.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
