# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug !strip)

pkgname=glib-networking
pkgver=2.70.0
pkgrel=0.1
epoch=1
pkgdesc="Network extensions for GLib"
url="https://gitlab.gnome.org/GNOME/glib-networking"
arch=(x86_64)
license=(GPL2)
depends=("glib2>=2.69.0" libproxy gnutls gsettings-desktop-schemas)
makedepends=(meson git)
checkdepends=(ca-certificates)
_commit=b889a3e23bcc0b754334f16c1b60469d1a5a0933  # tags/2.70.0^0
source=("git+https://gitlab.gnome.org/GNOME/glib-networking.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # gnutls: fix use of non-default GTlsDatabases
  # https://gitlab.gnome.org/GNOME/glib-networking/-/issues/169
  git cherry-pick -n d53e3a610f0b3eb57492d8dc24f1d2d1e7a2ec30

}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
