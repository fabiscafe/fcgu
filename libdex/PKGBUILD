# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgbase=libdex
pkgname=(libdex)
pkgver=0.000000000000+r446+a9b09f8
pkgrel=1
pkgdesc="A library supporting 'Deferred Execution'"
url="https://gitlab.gnome.org/chergert/libdex"
arch=(x86_64)
license=(LGPL2.1)
depends=(
  gobject-introspection-runtime
  libsoup3
  liburing
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_commit=a9b09f891b073b00fd6ca5499db5afc5afb7f80b  # main^0
source=("git+https://gitlab.gnome.org/chergert/libdex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  printf "0.000000000000+r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
    -D examples=false
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() (
  meson test -C build --print-errorlogs
)

package_libdex() {
  meson install -C build --destdir "$pkgdir"
}
