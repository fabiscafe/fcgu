# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-tour
pkgver=43.beta
pkgrel=1
pkgdesc='Guided tour and greeter for GNOME'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/gnome-tour'
license=('GPL3')
depends=('libadwaita')
makedepends=('appstream-glib' 'meson' 'rust' 'git')
_commit=ed523b73e6fa81efbcb72ca66f0bcc760582cf1d  # tags/43.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-tour.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
