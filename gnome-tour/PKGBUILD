# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-tour
pkgver=44.0
pkgrel=0.1
pkgdesc='Guided tour and greeter for GNOME'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/gnome-tour'
license=('GPL3')
depends=(
  'gtk4'
  'libadwaita'
)
makedepends=(
  'appstream-glib'
  'git'
  'meson'
  'rust'
)
_commit=46ca411004a49ef963e3a7df7b928ebb976bed72  # tags/44.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-tour.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
