# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gtksourceview5
pkgver=5.1.0
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
url='https://gitlab.gnome.org/GNOME/gtksourceview'
license=('LGPL')
arch=(x86_64)
depends=(gtk4)
makedepends=(git gobject-introspection
             gtk-doc meson vala)
checkdepends=(xorg-server-xvfb)
_commit=8a0d4c0334004b5b01cb794e6b32edde5b5308d4  # tags/5.1.0^0
source=("git+https://gitlab.gnome.org/GNOME/gtksourceview.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtksourceview
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson gtksourceview build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir="${pkgdir}"
}
