# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gtksourceview5
pkgver=5.4.0
pkgrel=0.1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
url='https://gitlab.gnome.org/GNOME/gtksourceview'
license=('LGPL')
arch=(x86_64)
depends=(gtk4)
makedepends=(git gi-docgen gobject-introspection
             gtk-doc meson vala)
checkdepends=(xorg-server-xvfb)
_commit=785a794811bc48505fa1c2c7b970ca2203cf759a  # tag/5.4.0^0
source=("git+https://gitlab.gnome.org/GNOME/gtksourceview.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtksourceview
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson gtksourceview build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
