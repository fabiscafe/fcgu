# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libgweather-4
pkgname=(libgweather-4 libgweather-4-docs)
pkgver=4.0.0
pkgrel=1.3
pkgdesc="Location and timezone database and weather-lookup library"
url="https://wiki.gnome.org/Projects/LibGWeather"
arch=(x86_64)
license=(LGPL)
depends=(libsoup3 geocode-glib-2 dconf)
makedepends=(gobject-introspection vala git gi-docgen meson python-gobject)
checkdepends=(python-pylint)
options=(debug)
_commit=6ac9872a5a06863f6c84f6ac836cede78b401691  # tags/4.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/libgweather.git#commit=$_commit"
        "geocode-glib-2.patch::https://gitlab.gnome.org/GNOME/libgweather/-/commit/bf6794eb099a3984dd23d578cabaae9c63b9f9d0.patch"
        disable-metar-test.diff)
sha256sums=('SKIP'
            '05fc435867101ff59f59b3422b7dea7e0ac53504981803ab8911f2628dba1892'
            'c11eb9ad69f8354e5baeef5581038ef0cd5ce2f032dd48f3df29e34fc900dd5a')

pkgver() {
  cd libgweather
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libgweather

  # Disable metar test, depends on unstable online service
  git apply -3 ../disable-metar-test.diff
  # change the dependencies on geocode-glib to make it build
  git apply -3 ../geocode-glib-2.patch
}

build() {
  arch-meson libgweather build \
    -D gtk_doc=true \
    -D soup2=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libgweather-4() {
  provides=(libgweather-4.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libgweather-4-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 et:
