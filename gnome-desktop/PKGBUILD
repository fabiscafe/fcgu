# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jan@archlinux.org>
options=(debug)

pkgname=gnome-desktop
pkgver=42.0
pkgrel=0.1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
url="https://gitlab.gnome.org/GNOME/gnome-desktop"
arch=(x86_64)
license=(GPL LGPL)
depends=(gsettings-desktop-schemas gtk3 gtk4 xkeyboard-config iso-codes libseccomp
         bubblewrap systemd libxkbcommon)
makedepends=(gobject-introspection git meson gtk-doc yelp-tools)
provides=(libgnome-desktop-3.so libgnome-desktop-4.so)
_commit=c3e756b28ddfeda0fa061d51cb11e47e09791770  # tag/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-desktop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D gnome_distributor="Arch Linux" \
    -D gtk_doc=true \
    -D debug_tools=false \
    -D date_in_gnome_version=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
