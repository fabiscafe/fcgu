# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
options=(debug !strip '!emptydirs')

pkgname=gnome-connections
pkgver=41.0
pkgrel=0.1
pkgdesc='Remote desktop client for the GNOME desktop environment'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/connections'
license=('GPL3')
depends=('freerdp' 'gtk-vnc' 'libhandy')
makedepends=('appstream-glib' 'git' 'gobject-introspection' 'itstool' 'meson' 'vala')
groups=('gnome-extra')
_commit=467d5044acc0aee5d4638505a49307015728784a  # tags/41.0^0
source=("git+https://gitlab.gnome.org/GNOME/connections.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd connections
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson connections build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  # Remove unneeded development files
  rm -r "$pkgdir"/usr/include/gnome-connections/gtk-frdp \
        "$pkgdir"/usr/lib/gnome-connections/{girepository-1.0,pkgconfig} \
        "$pkgdir"/usr/share/gnome-connections/{gir-1.0,vapi}
}
