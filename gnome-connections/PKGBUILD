# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
options=('!emptydirs' 'debug')

pkgname=gnome-connections
pkgver=42.1.2
pkgrel=0.1
pkgdesc='Remote desktop client for the GNOME desktop environment'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/connections'
license=('GPL3')
depends=('freerdp' 'gtk-vnc' 'libhandy' 'libsecret')
makedepends=('appstream-glib' 'git' 'gobject-introspection' 'itstool' 'meson' 'vala')
groups=('gnome-extra')
_commit=6ce30d5bff390055e73a97d272c4b777a87ecf65  # tag/42.1.2^0
source=("git+https://gitlab.gnome.org/GNOME/connections.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname/gnome-/}
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ${pkgname/gnome-/}
}

build() {
  arch-meson ${pkgname/gnome-/} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
