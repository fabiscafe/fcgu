# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=gnome-backgrounds
pkgver=42.beta.0
pkgrel=1
pkgdesc="Background images and data for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-backgrounds"
arch=(any)
makedepends=(glib2 meson git)
license=(GPL)
groups=(gnome)
_commit=26aaaea877eceea60b1066bd1fedbd3732d0153c # tags/42.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-backgrounds.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/[^-]*-g/r&/;s/-/+/g;s/beta/beta.0/'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
