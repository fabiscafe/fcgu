# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-backgrounds
pkgver=43.0
pkgrel=0.1
pkgdesc="Background images and data for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-backgrounds"
arch=(any)
# To show webp backgrounds in gnome-shell
depends=(webp-pixbuf-loader)
makedepends=(git glib2 meson)
license=(GPL)
groups=(gnome)
options=(debug)
_commit=3dc06f83fda4169432a43443cfa1617bbbd4810b  # tags/43^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-backgrounds.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r '/\.([0-9])/!s/$/\.0/;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
