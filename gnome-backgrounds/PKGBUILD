# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug !strip)

pkgname=gnome-backgrounds
pkgver=41.0
pkgrel=0.1
pkgdesc="Background images and data for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-backgrounds"
arch=(any)
makedepends=(glib2 meson)
license=(GPL)
groups=(gnome)
_tarver=${pkgver/rc/.rc}
source=(https://download.gnome.org/sources/$pkgname/${_tarver%%.*}/$pkgname-$_tarver.tar.xz)
sha256sums=('1da1ac0d261bedf0fcd2c85b480bc65505e23cf51f1143126c0d37717e693145')

build() {
  arch-meson $pkgname-$_tarver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
