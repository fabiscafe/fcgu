# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=gnome-characters
pkgver=42.beta.0
pkgrel=1
pkgdesc="A character map application"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/gnome-characters"
license=(GPL2)
depends=(gjs gtk4 libadwaita libunistring gnome-desktop emoji-font)
makedepends=(meson gobject-introspection appstream-glib git gperf)
groups=(gnome)
_commit=0c3410de876f7a9165f4e1f93df959a8e83e3e4a  # tags/42.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-characters.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/beta/beta.0/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
