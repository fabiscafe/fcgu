# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgbase=feedbackd
pkgname=(${pkgbase} ${pkgbase}-doc)
pkgver=0.0.3
pkgrel=0.1
pkgdesc="A daemon to provide haptic (and later more) feedback on events"
arch=(x86_64)
url="https://source.puri.sm/Librem5/feedbackd"
license=(GPL3)
options=(debug)
depends=(gsound libgudev json-glib)
makedepends=(gi-docgen git gobject-introspection meson vala)
_commit=25913a6aad24e612d56a74aa6a7f280020a4e5a5  # tags/v0.0.3^0
source=("git+https://source.puri.sm/Librem5/feedbackd.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_feedbackd() {
  meson install -C build --destdir "${pkgdir}"

  # Split up docs
  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_feedbackd-doc() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
