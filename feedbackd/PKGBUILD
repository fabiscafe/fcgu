# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=feedbackd
pkgver=0.0.0+git20211018
pkgrel=0.1
pkgdesc="A daemon to provide haptic (and later more) feedback on events"
arch=(x86_64)
url="https://source.puri.sm/Librem5/feedbackd"
license=(GPL3)
depends=(gsound libgudev json-glib)
makedepends=(git gobject-introspection meson vala)
_commit=753fff3e7ae1d0bce4f58cef721e45c50c404786  # tags/0.0.0+git20211018^0
source=("git+https://source.puri.sm/Librem5/feedbackd.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g;s/^v//g'
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
