# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgbase=gnome-software
pkgname=(
  gnome-software
  gnome-software-packagekit-plugin
)
pkgver=44.0
pkgrel=0.1
pkgdesc="GNOME Software Tools"
url="https://wiki.gnome.org/Apps/Software/"
arch=(x86_64)
license=(GPL)
makedepends=(
  appstream
  docbook-xsl
  flatpak
  fwupd
  git
  gnome-online-accounts
  gobject-introspection
  gsettings-desktop-schemas
  gtk4
  gtk-doc
  libadwaita
  libglib-testing
  libpackagekit-glib
  libsoup3
  libsysprof-capture
  libxmlb
  malcontent
  meson
)
_commit=5ecb94a720ca304d0e6fa7071a13cb4f1e69e158  # tags/44.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-software.git#commit=$_commit"
  "git+https://gitlab.gnome.org/mwleeds/gnome-pwa-list.git"
)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase

  git submodule init
  git submodule set-url subprojects/gnome-pwa-list "$srcdir/gnome-pwa-list"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson $pkgbase build
  meson compile -C build
}

# Not running tests - need root and a D-Bus system bus

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gnome-software() {
  groups=(gnome)
  depends=(
    appstream
    gnome-online-accounts
    gsettings-desktop-schemas
    gtk4
    libadwaita
    libpackagekit-glib
    libsoup3
    libsysprof-capture
    libxmlb
  )
  optdepends=(
    'flatpak: Flatpak support plugin'
    'fwupd: fwupd support plugin'
    'malcontent: Parental control plugin'
  )

  meson install -C build --destdir "$pkgdir"

  local pkglibdir="$pkgdir/usr/lib/gnome-software"
  _pick packagekit-plugin "$pkglibdir"/plugins-*/libgs_plugin_packagekit*.so
}

package_gnome-software-packagekit-plugin() {
  pkgdesc="PackageKit support plugin for GNOME Software"
  depends=(
    archlinux-appstream-data
    gnome-software
    packagekit
  )
  mv packagekit-plugin/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
