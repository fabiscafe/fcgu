# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=nautilus
pkgname=(nautilus libnautilus-extension)
pkgver=43.0
pkgrel=0.2
pkgdesc="Default file manager for GNOME"
url="https://wiki.gnome.org/Apps/Files"
arch=(x86_64)
license=(GPL)
depends=(libgexiv2 gnome-desktop-4 gvfs dconf tracker3 tracker3-miners
         gnome-autoar gst-plugins-base-libs libadwaita libportal-gtk4)
makedepends=(gobject-introspection git gi-docgen meson libcloudproviders appstream-glib)
checkdepends=(python-gobject)
options=(debug)
_commit=4e4c1e8a729d02d293bf39f6caec61fd72ac1d0a  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/nautilus.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed -r 's/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  arch-meson $pkgbase build \
    -D docs=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_nautilus() {
  depends+=(libnautilus-extension libcloudproviders)
  optdepends=('nautilus-sendto: Send files via mail extension'
              'nextcloud-client-cloudproviders: Nextcloud integration')
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"

  _pick libne usr/include
  _pick libne usr/lib/{girepository-1.0,libnautilus-extension*,pkgconfig}
  _pick libne usr/share/{gir-1.0,doc}
}

package_libnautilus-extension() {
  pkgdesc="Library for extending the $pkgdesc"
  depends=(gtk4)
  provides=(libnautilus-extension.so)
  mv libne/* "$pkgdir"
}

# vim:set sw=2 et:
