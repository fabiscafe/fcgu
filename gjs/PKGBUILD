# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
options=(debug)

pkgname=gjs
pkgver=1.71.1
pkgrel=2
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js91 dconf readline
         libsysprof-capture)
makedepends=(gobject-introspection git meson dbus)
checkdepends=(valgrind xorg-server-xvfb gtk3 gtk4)
provides=(libgjs.so)
_commit=a4a17e6b4df58bbfc08132e90e5910bd46c2a2cf  # tags/1.71.1^0
source=("git+https://gitlab.gnome.org/GNOME/gjs.git#commit=$_commit"
        "fix-cairo-test.patch::https://gitlab.gnome.org/GNOME/gjs/-/merge_requests/724.patch")
sha256sums=('SKIP'
            'f0cc65109d14c2fdc7ccc0cac4530eb2a517556e7a3e5580a195eb50f424a4ab')

pkgver() {
  cd gjs
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gjs
  Bug: https://gitlab.gnome.org/GNOME/gjs/-/issues/461
  Patch: https://gitlab.gnome.org/GNOME/gjs/-/merge_requests/724
  git apply -3 ../fix-cairo-test.patch
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  CXXFLAGS="${CXXFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson gjs build -D installed_tests=false
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libreadline.so)
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
