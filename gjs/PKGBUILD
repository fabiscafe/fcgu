# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
options=(debug)

pkgname=gjs
pkgver=1.70.1+r181+g21729c92
pkgrel=1
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js91 dconf readline
         libsysprof-capture)
makedepends=(gobject-introspection git meson dbus)
checkdepends=(valgrind xorg-server-xvfb gtk3 gtk4)
provides=(libgjs.so)
_commit=21729c92538a36a75b524bbcd1e6b07bdfa9028d  # tags/master^0
source=("git+https://gitlab.gnome.org/GNOME/gjs.git#commit=$_commit"
        "fix_regression_converting_number_to_64-bit_int.patch::https://gitlab.gnome.org/GNOME/gjs/-/merge_requests/721.patch")
sha256sums=('SKIP'
            '3aa77d52acf36a8db408dda3fc34f3c54ba578a3a7fb21101d0da10d151d2380')

pkgver() {
  cd gjs
  git describe --tags | sed 's/70.0/70.1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gjs
  # Bug: https://gitlab.gnome.org/GNOME/gjs/-/issues/459
  # Fix: https://gitlab.gnome.org/GNOME/gjs/-/merge_requests/721
  git apply -3 ../fix_regression_converting_number_to_64-bit_int.patch
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  CXXFLAGS="${CXXFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson gjs build -D installed_tests=false
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libreadline.so)
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
