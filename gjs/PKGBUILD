# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
options=(debug)

pkgname=gjs
pkgver=1.70.1+r191+gfdbeccc8
pkgrel=1
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js91 dconf readline
         libsysprof-capture)
makedepends=(gobject-introspection git meson dbus)
checkdepends=(valgrind xorg-server-xvfb gtk3 gtk4)
provides=(libgjs.so)
_commit=fdbeccc8adc8e2c4cc7e57eeb770738e5ae92b56  # tags/master^0
source=("git+https://gitlab.gnome.org/GNOME/gjs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gjs
  git describe --tags | sed 's/70.0/70.1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gjs
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  CXXFLAGS="${CXXFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson gjs build -D installed_tests=false
  meson compile -C build
}

check() {
  # Cairo test broken
  # Bug: https://gitlab.gnome.org/GNOME/gjs/-/issues/461
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs ||:
}

package() {
  depends+=(libreadline.so)
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
