# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
options=(debug)

pkgname=gnome-secrets
pkgver=6.0
pkgrel=0.1
pkgdesc="Secrets is a password manager which makes use of the KeePass v.4 format."
arch=(any)
url="https://gitlab.gnome.org/World/secrets"
license=(GPL3)
provides=("gnome-passwordsafe")
replaces=("gnome-passwordsafe")
conflicts=("gnome-passwordsafe")
depends=(libadwaita libpwquality python-pykeepass python-pyotp)
makedepends=(git gobject-introspection meson)
_commit=3bd864d0fb61ff5461e89e14834faa61955dfcaf
source=("git+https://gitlab.gnome.org/World/secrets.git/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd secrets
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson secrets build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
