# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
options=(debug)

pkgname=gnome-secrets
pkgver=6.0beta
pkgrel=1
pkgdesc="A password manager which integrates perfectly with the GNOME desktop and provides an easy and uncluttered interface for the management of password databases."
arch=(any)
url="https://gitlab.gnome.org/World/secrets"
license=(GPL3)
provides=("gnome-passwordsafe")
replaces=("gnome-passwordsafe")
conflicts=("gnome-passwordsafe")
depends=(libadwaita libpwquality python-pykeepass python-pyotp)
makedepends=(git gobject-introspection meson)
_commit=d7622620da44c8c1f36cf8f1fee4a9fccc66c6a0
source=("git+https://gitlab.gnome.org/World/secrets.git/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd secrets
  git describe --tags | sed 's/-/+/g;s/.beta/beta/;s/.rc/rc/'
}

build() {
  arch-meson secrets build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
