# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem
pkgver=43.rc
pkgrel=3
pkgdesc="Movie player for the GNOME desktop based on GStreamer"
url="https://wiki.gnome.org/Apps/Videos"
arch=(x86_64)
license=(GPL2 custom)
depends=(totem-plparser iso-codes libpeas grilo gsettings-desktop-schemas
         python-gobject gnome-desktop gst-plugins-base gst-plugins-good
         gst-plugins-bad gst-plugin-gtk libhandy libportal-gtk3)
makedepends=(libnautilus-extension itstool docbook-xsl gtk-doc git
             gobject-introspection appstream-glib meson intltool)
checkdepends=(xorg-server-xvfb python-pylint)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'grilo-plugins: Media discovery')
conflicts=(totem-plugin)
replaces=(totem-plugin)
groups=(gnome)
options=(debug)
_commit=29f555d053d44b55971bcba76821ac436090cbd3  # tags/43.rc^0
source=("git+https://gitlab.gnome.org/GNOME/totem.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git"
        "0001_fix_tests_with_pylint_2.14.5.diff")
sha256sums=('SKIP'
            'SKIP'
            'f575887c48939a0f1574e5189ef7fcc40e282078628817097c7b47497fccdfe0')

pkgver() {
  cd totem
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd totem

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git submodule update

  git apply -3 ../0001_fix_tests_with_pylint_2.14.5.diff
}

build() {
  arch-meson totem build \
    -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  # Skip pylint-pythonconsole test as it fails with pylint>=2.14
  # https://gitlab.gnome.org/GNOME/totem/-/issues/537
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 totem/COPYING
}
