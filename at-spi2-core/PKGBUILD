# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-core
pkgver=2.45.1
pkgrel=3
pkgdesc="Base DBus XML interfaces for accessibility, the accessibility registry daemon, and atspi library."
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
arch=(x86_64)
license=(GPL2)
depends=(dbus glib2 libxtst libsystemd)
makedepends=(gobject-introspection git gtk-doc meson dbus-broker systemd)
optdepends=('dbus-broker: Alternative bus implementation')
options=(debug)
conflicts=(atk at-spi2-atk)
replaces=(atk at-spi2-atk)
provides=(atk at-spi2-atk)
_commit=4d636769923c685db412fecf475c80597e328e25  # tags/AT_SPI2_CORE_2_45_1^0
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D default_bus=dbus-broker \
    -D docs=true
  meson compile -C build
}

check() {
  # memory test fails without desktop
  # broker fails to launch without journald
  dbus-run-session meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
