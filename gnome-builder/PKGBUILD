# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=gnome-builder
pkgname=(gnome-builder)
pkgver=44.0
pkgrel=0.1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(
  autoconf-archive
  clang
  cmark
  ctags
  desktop-file-utils
  devhelp
  d-spy
  editorconfig-core-c
  flatpak-builder
  gjs
  glib2
  gspell
  gtksourceview5
  jsonrpc-glib
  libadwaita
  libdex
  libgit2-glib
  libpanel
  libpeas
  libportal-gtk4
  meson
  python-gobject
  python-lxml
  python-sphinx
  sysprof
  template-glib
  vala
  vte4
  webkitgtk-6.0
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  jedi-language-server
  llvm
  mm-common
  python-sphinx_rtd_theme
  yelp-tools
)
checkdepends=(weston)
_commit=f92ddd5a6aa4dbab2044c8189dec5856b4b145d4  # tags/44.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit"
  "PATCH_Revert_Fix_adw_expander_row_add_action_deprecation.patch::https://gitlab.gnome.org/GNOME/gnome-builder/-/commit/d89a5c95e43f4a4e1a4e9d063e3962bac8e51db0.patch"
)
sha256sums=('SKIP'
            '1ab1afeaac1e58dc52b8d26f495b233b9f85e1ae07837bc82c120537e30eb6c1')

pkgver() {
  cd $pkgbase
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
  # FTB Builder 44.0: error: implicit declaration of function ‘adw_expander_row_add_suffix’
  ## https://gitlab.gnome.org/GNOME/gnome-builder/-/issues/2010
  git apply "$srcdir/PATCH_Revert_Fix_adw_expander_row_add_action_deprecation.patch"
}

build() {
  local meson_options=(
    -D help=true
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir" WAYLAND_DISPLAY=wayland-5

  mkdir -p -m 700 "$XDG_RUNTIME_DIR"
  weston --backend=headless-backend.so --socket=$WAYLAND_DISPLAY --idle-time=0 &
  _w=$!

  trap "kill $_w; wait" EXIT

  dbus-run-session meson test -C build --print-errorlogs
)

package_gnome-builder() {
  depends+=(libgit2.so)
  optdepends=(
    'jedi-language-server: Python code assistance'
    'bash-language-server: Shell code assistance'
  )
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
