# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=gnome-builder
# docs fail to build
# https://gitlab.gnome.org/GNOME/gnome-builder/-/issues/1793
#pkgname=(gnome-builder gnome-builder-libide-docs)
pkgname=gnome-builder
pkgver=43.2
pkgrel=0.2
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(editorconfig-core-c gtksourceview5 libgit2-glib gjs python-gobject sysprof
         desktop-file-utils ctags libpeas vte3 autoconf-archive libadwaita
         libdazzle template-glib jsonrpc-glib python-sphinx webkit2gtk-5.0
         python-lxml meson libportal-gtk4 cmark clang flatpak-builder libpanel libdspy)
makedepends=(llvm gobject-introspection gi-docgen yelp-tools appstream-glib git
             mm-common python-sphinx_rtd_theme jedi-language-server)
checkdepends=(xorg-server-xvfb)
options=(debug)
_commit=1df3608776a11ddfa8f868c4f2a86b3d5aa71e5e  # tags/43.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  arch-meson $pkgbase build \
    -D docs=false \
    -D help=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

#package_gnome-builder() {
package() {
  depends+=(libgladeui-2.so)
  optdepends=('gnome-code-assistance: Legacy assistance services'
              'jedi-language-server: Python code assistance')
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

#  _pick libide-docs "$pkgdir"/usr/share/doc/libide
}

#package_gnome-builder-libide-docs() {
#  pkgdesc+=" (libide API documentation)"
#  depends=()
#  mv libide-docs/* "$pkgdir"
#}

# vim:set sw=2 et:

