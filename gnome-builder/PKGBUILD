# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=gnome-builder
pkgname=(gnome-builder)
pkgver=44alpha
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtk4 "glib2>=2.75.0" gtksourceview5 devhelp libgit2-glib gjs python-gobject sysprof
         desktop-file-utils ctags libpeas vte4 vala autoconf-archive libadwaita
         gspell libpanel template-glib jsonrpc-glib python-sphinx webkitgtk-6.0
         python-lxml meson libportal-gtk4 cmark clang flatpak-builder
         editorconfig-core-c d-spy)
makedepends=(llvm gobject-introspection yelp-tools appstream-glib git mm-common
             python-sphinx_rtd_theme jedi-language-server)
checkdepends=(weston)
options=(debug)
_commit=6d0a1230ebc18dc281023912dfdbdcc4fcc1491b  # tags/44.alpha^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
    -D help=true
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir" WAYLAND_DISPLAY=wayland-5

  mkdir -p -m 700 "$XDG_RUNTIME_DIR"
  weston --backend=headless-backend.so --socket=$WAYLAND_DISPLAY --idle-time=0 &
  _w=$!

  trap "kill $_w; wait" EXIT

  dbus-run-session meson test -C build --print-errorlogs ||:
)

package_gnome-builder() {
  depends+=(libgit2.so)
  optdepends=('jedi-language-server: Python code assistance'
              'bash-language-server: Shell code assistance')
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
