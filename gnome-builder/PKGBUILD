# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug !strip)

pkgname=gnome-builder
pkgver=41.0
pkgrel=0.1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject sysprof
         desktop-file-utils ctags libpeas vte3 vala autoconf-archive
         flatpak-builder gspell libdazzle template-glib jsonrpc-glib
         python-sphinx webkit2gtk glade python-lxml meson libportal jedi-language-server cmark)
makedepends=(llvm gobject-introspection gtk-doc yelp-tools appstream-glib git
             mm-common python-sphinx_rtd_theme clang gi-docgen)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-code-assistance: Legacy assistance services'
            'clang: C/C++ code assistance')
groups=(gnome-extra)
_commit=426838bea03ec94d7e658c681ed9155e4a9a41a7 # tags/41.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D help=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs || true
   # Validate appstream file FAIL https://gitlab.gnome.org/GNOME/gnome-builder/-/issues/1526
}

package() {
  depends+=(libgladeui-2.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
