# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=libvirt-glib
pkgver=4.0.0
pkgrel=0.1
pkgdesc="GLib bindings for libvirt"
arch=('x86_64')
url="https://libvirt.org"
license=('LGPL2.1')
depends=('libvirt' 'libcap-ng' 'gobject-introspection-runtime')
makedepends=('intltool' 'gobject-introspection' 'meson' 'vala' 'python')
optdepends=('python: support for python')
source=("https://libvirt.org/sources/glib/$pkgname-$pkgver.tar.xz"{,'.asc'})
sha512sums=('e1b99e80aa671fa5a4d7f7f7a4ea70ce7c5fdaf97574407f79aa11a75dd177e1e3f92027728a83ded62e305852bbda76eb90326aaa1f5f19b77272b008351810'
            'SKIP')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF') # Daniel P. Berrange <dan@berrange.com>

build() {
  CPPFLAGS+=" -O2" #warning _FORTIFY_SOURCE requires compiling with optimization (-O)
  cd "${srcdir}"
  arch-meson ${pkgname}-${pkgver} build
  meson compile -C build
}

package() {
  cd "${srcdir}"
  DESTDIR="${pkgdir}" meson install -C build
}

# vim:set ts=2 sw=2 et:
