# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-system-monitor
pkgver=40.0
pkgrel=0.1
pkgdesc="View current processes and monitor system state"
url="https://wiki.gnome.org/Apps/SystemMonitor"
arch=(x86_64)
license=(GPL)
depends=(libgtop gtkmm3 librsvg systemd libhandy)
makedepends=(meson yelp-tools git)
groups=(gnome)
_commit=24e810d54e21d7be34be9807d025f77ef0295193  # tags/40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-system-monitor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
