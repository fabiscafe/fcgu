# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=loupe
pkgver=0.0.0+r152+ccfbcf6
pkgrel=1
pkgdesc="A simple image viewer for GNOME (pre-release)"
url="https://gitlab.gnome.org/BrainBlasted/loupe"
arch=(any)
license=(GPL)
depends=(libadwaita)
makedepends=(git itstool meson rust)
options=(debug)
_commit=ccfbcf60a8f5f2c20bf48b5efd2efc3f01315d32  # main^0
source=("git+https://gitlab.gnome.org/BrainBlasted/loupe.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "0.0.0+r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
