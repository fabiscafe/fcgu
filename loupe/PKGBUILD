# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=loupe
pkgver=0.0.0+r285+b60656c
pkgrel=1
pkgdesc="A simple image viewer for GNOME (pre-release)"
url="https://gitlab.gnome.org/Incubator/loupe"
arch=(x86_64)
license=(GPL)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libgweather-4
  libheif
  libxml2
  pango
)
makedepends=(
  git
  itstool
  meson
  rust
)
_commit=b60656cf5ce3dd23706c6bfd684c5bd33d516913  # main^0
source=("git+https://gitlab.gnome.org/Incubator/loupe.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "0.0.0+r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
