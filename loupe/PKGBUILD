# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=loupe
pkgver=44.0
pkgrel=0.1
pkgdesc="A simple image viewer for GNOME"
url="https://gitlab.gnome.org/Incubator/loupe"
arch=(x86_64)
license=(GPL)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libgweather-4
  libheif
  libxml2
  pango
)
makedepends=(
  git
  itstool
  meson
  rust
)
_commit=1b184868263af998b03a2b5f1b57e8fbf041bc21  # tags/44.0^0
source=("git+https://gitlab.gnome.org/Incubator/loupe.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
