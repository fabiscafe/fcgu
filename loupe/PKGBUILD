# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=loupe
pkgver=0.0.0+r189+aa7a2df
pkgrel=1
pkgdesc="A simple image viewer for GNOME (pre-release)"
url="https://gitlab.gnome.org/Incubator/loupe"
arch=(any)
license=(GPL)
depends=(libadwaita libgweather-4)
makedepends=(git itstool meson rust)
options=(debug)
_commit=aa7a2dfd931e9fdb66297591866a311d4eb7ba47  # main^0
source=("git+https://gitlab.gnome.org/Incubator/loupe.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "0.0.0+r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
