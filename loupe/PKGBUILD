# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=loupe
pkgver=0.0.0+r296+36188ae
pkgrel=1
pkgdesc="A simple image viewer for GNOME (pre-release)"
url="https://gitlab.gnome.org/Incubator/loupe"
arch=(x86_64)
license=(GPL)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
  libgweather-4
  libheif
  libxml2
  pango
)
makedepends=(
  git
  itstool
  meson
  rust
)
_commit=36188aece7e168cbf02f7405f04e0058263c4d34  # main^0
source=("git+https://gitlab.gnome.org/Incubator/loupe.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "0.0.0+r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
