# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug)

pkgname=glibc-tools
pkgver=0.1+r3.23fd2b9
pkgrel=1
pkgdesc="Tools and library provided by glibc that either have been deprectated of moved out from the project"
url="https://github.com/zatrazz/glibc-tools"
arch=(x86_64)
license=(GPL2)
depends=(bash "glibc>=2.35")
makedepends=(git)
_commit=23fd2b927e159a8a32905f3fea0af3743349133c  # branch:main^0
source=("git+https://github.com/zatrazz/glibc-tools.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "%s+r%s.%s" \
    "$(grep 'version=' configure | cut -d= -f2)" \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
