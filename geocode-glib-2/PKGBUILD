# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=geocode-glib-2
pkgver=3.26.4
pkgrel=0.1
pkgdesc="Helper library for geocoding services"
url="https://gitlab.gnome.org/GNOME/geocode-glib"
arch=(x86_64)
license=(LGPL)
depends=(geocode-glib glib2 json-glib libsoup3)
makedepends=(gobject-introspection git gtk-doc meson)
provides=(libgeocode-glib-2.so)
options=(debug)
_commit=210abe69d68e38947106f4680631c369b0c23189  # tags/3.26.4^0
source=("git+https://gitlab.gnome.org/GNOME/geocode-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname/-2//}
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ${pkgname/-2//}
}

build() {
  arch-meson ${pkgname/-2//} build \
    -D enable-installed-tests=false \
    -D soup2=false
  meson compile -C build
}

check() {
  # Requires locales not available at buildtime, in a clean environment
  # https://gitlab.gnome.org/GNOME/geocode-glib/-/commit/c9bbf13d76600492565fa042638b48cb737e492d
  meson test -C build --print-errorlogs ||:
}

package() {
  meson install -C build --destdir "$pkgdir"
  
  # Exclude the icons, they are provided by geocode-glib
  rm -r "$pkgdir/usr/share/icons"
}

