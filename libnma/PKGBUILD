# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=libnma
pkgver=1.8.36
pkgrel=0.1
pkgdesc="NetworkManager GUI client library"
url="https://gitlab.gnome.org/GNOME/libnma"
arch=(x86_64)
license=(GPL LGPL)
depends=(libnm gcr gtk3 gtk4 iso-codes mobile-broadband-provider-info)
makedepends=(gobject-introspection git gtk-doc meson vala)
provides=(libnma.so libnma-gtk4.so libnma-common libnma-gtk4)
replaces=(libnma-common libnma-gtk4)
conflicts=(libnma-common libnma-gtk4)
_commit=d3080c86c6cfb71d16514af4d967d36dba976206  # tags/1.8.36^0
source=("git+https://gitlab.gnome.org/GNOME/libnma.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libnma
  git describe --tags | sed 's/-dev//g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libnma
}

build() {
  arch-meson libnma build \
    -D libnma_gtk4=true
  # -D gcr_gtk4=true # Waiting for a gcr release
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
