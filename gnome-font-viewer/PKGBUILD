# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=gnome-font-viewer
pkgver=42.beta.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-font-viewer"
arch=(x86_64)
license=(GPL)
depends=(gnome-desktop libadwaita)
makedepends=(git meson)
groups=(gnome)
_commit=ca3289890e5178ef2ca417d8266ca1c21c327ec4  # tags/42.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-font-viewer.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/beta/beta.0/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
