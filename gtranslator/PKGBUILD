# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Rafael Fontenelle
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=42.0
pkgrel=0.1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('x86_64')
url='https://projects.gnome.org/gtranslator/'
license=('GPL3')
options=(debug)
depends=('gtksourceview4' 'gspell' 'gettext' 'libgda6' 'libhandy')
makedepends=('meson' 'gobject-introspection' 'gtk-doc' 'yelp-tools')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('2a67bcbfe643061b0696d89a10847d50bf35905ff3361b9871357d3e3422f13b')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  arch-meson $pkgname-$pkgver build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

