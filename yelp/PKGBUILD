# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=yelp
pkgver=42.0
pkgrel=0.1
pkgdesc="Get help with GNOME"
url="https://wiki.gnome.org/Apps/Yelp"
license=(GPL)
arch=(x86_64)
depends=(bzip2 libhandy libxslt webkit2gtk-4.1 xz yelp-xsl)
makedepends=(gtk-doc itstool autoconf-archive git appstream-glib)
groups=(gnome)
_commit=45cffe678cd32a10885c3a845965accbc0313600  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/yelp.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc --enable-compile-warnings=minimum
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
