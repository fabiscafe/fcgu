# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug !strip)

pkgname=yelp
pkgver=41.1
pkgrel=0.1
pkgdesc="Get help with GNOME"
url="https://wiki.gnome.org/Apps/Yelp"
license=(GPL)
arch=(x86_64)
depends=(webkit2gtk yelp-xsl libxslt xz bzip2)
makedepends=(gtk-doc itstool autoconf-archive git appstream-glib)
groups=(gnome)
_commit=29b1db90c2ca491b95bae07f9a3a7d3b609eed4f  # tags/41.1^0
source=("git+https://gitlab.gnome.org/GNOME/yelp.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc --enable-compile-warnings=minimum \
    --with-webkit2gtk-4-0
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
