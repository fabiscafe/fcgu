# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
options=(debug)

pkgname=gnome-podcasts-gtk4
pkgver=0.5.0+beta+84+gf31ad03
pkgrel=1
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/msandova/podcasts"
license=(GPL3)
depends=(gst-plugins-bad gst-plugins-base gst-plugins-good libadwaita)
makedepends=(appstream-glib git meson rust)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
conflicts=(gnome-podcasts)
provides=(gnome-podcasts)
_commit=f31ad037bfc9c916d38c318d27bfa1a6334165f6 # gtk4^0
source=("git+${url}.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd podcasts
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson podcasts build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir="${pkgdir}"
}
