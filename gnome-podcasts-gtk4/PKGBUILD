# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
options=(debug)

pkgname=gnome-podcasts-gtk4
pkgver=0.5.1+r25+gc3c7725+mr199
pkgrel=1
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/World/podcasts"
license=(GPL3)
depends=(gst-plugins-bad gst-plugins-base gst-plugins-good libadwaita)
makedepends=(appstream-glib git meson rust)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
conflicts=(gnome-podcasts)
provides=(gnome-podcasts)
_commit=c3c77254cceabc0a417d157c5ee5aa55f24007b4 # master+patch^0
source=("git+${url}.git#commit=${_commit}"
        "mr199_gtk4-port.patch::${url}/-/merge_requests/199.patch")
sha256sums=('SKIP'
            '968e821e8aa1ab14e6fec46879854e88f81e24b73c9b5192bfb5b2a465ca5f9e')

pkgver() {
  cd podcasts
  printf "$(git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g')+mr199"
}

prepare() {
  cd podcasts
  # GTK4 port
  # https://gitlab.gnome.org/World/podcasts/-/merge_requests/199
  git apply -3 ../mr199_gtk4-port.patch
}

build() {
  arch-meson podcasts build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
