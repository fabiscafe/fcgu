# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
options=(debug)

pkgname=gnome-podcasts-gtk4
pkgver=0.5.0+beta+r101+ge93b92f
pkgrel=1
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/msandova/podcasts"
license=(GPL3)
depends=(gst-plugins-bad gst-plugins-base gst-plugins-good libadwaita)
makedepends=(appstream-glib git meson rust)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
conflicts=(gnome-podcasts)
provides=(gnome-podcasts)
_commit=e93b92f04e1247c46e058d0f0e219927b3e56dac # gtk4^0
source=("git+${url}.git#commit=${_commit}"
        "remove_incorrect_args_i18n.merge.patch")
sha256sums=('SKIP'
            '8a6e9d2e43311d636214529cc580de95fc1e955b619b998036b88d7e41a609a8')

pkgver() {
  cd podcasts
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd podcasts

  # https://gitlab.gnome.org/msandova/podcasts/-/merge_requests/1
  git apply -3 ../remove_incorrect_args_i18n.merge.patch
}

build() {
  arch-meson podcasts build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
