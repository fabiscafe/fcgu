# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=tepl
pkgver=6.0.0.0
pkgrel=0.1
pkgdesc="Library that eases the development of GtkSourceView-based text editors and IDEs"
url="https://wiki.gnome.org/Projects/Tepl"
arch=(x86_64)
license=(LGPL3)
depends=(amtk gtksourceview4 uchardet)
makedepends=(gobject-introspection gtk-doc vala git meson)
checkdepends=(xorg-server-xvfb)
_commit=51b15258e130d2b5134f6cd5ff7f91c75e0a482a  # tags/6.0.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/tepl.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libg{d,t}k-3.so libamtk-5.so
            libgtksourceview-4.so)
  provides+=("libtepl-${pkgver%%.*}.so")

  DESTDIR="$pkgdir" meson install -C build
}
