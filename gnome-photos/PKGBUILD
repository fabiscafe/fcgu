# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=gnome-photos
pkgver=42.0
pkgrel=0.1
epoch=1
pkgdesc="Access, organize, and share your photos on GNOME"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Photos"
license=(GPL)
depends=(exempi libexif librsvg babl gegl gsettings-desktop-schemas
         grilo-plugins lcms2 tracker3-miners geocode-glib grilo gfbgraph libgdata
         libgexiv2 libraw gnome-online-accounts dleyna-server dleyna-renderer
         libdazzle libhandy)
makedepends=(python yelp-tools git meson docbook-xsl)
groups=(gnome)
_commit=263d82c16b18db0f59c123e34f317a6926debdd8  # tags/42.0
source=("git+https://gitlab.gnome.org/GNOME/gnome-photos.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git submodule update
}

build() {
  arch-meson $pkgname build \
    -D manuals=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
