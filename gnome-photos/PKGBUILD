# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-photos
pkgver=43.0
pkgrel=0.1
epoch=1
pkgdesc="Access, organize, and share your photos on GNOME"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Photos"
license=(GPL)
depends=(exempi gtk3 libexif librsvg babl gegl gsettings-desktop-schemas
         grilo-plugins lcms2 tracker3-miners geocode-glib-2 grilo libgdata
         libgexiv2 libraw gnome-online-accounts dleyna-server dleyna-renderer
         libdazzle libhandy libportal-gtk3)
makedepends=(python yelp-tools git meson docbook-xsl)
groups=(gnome)
options=(debug)
_commit=79de8be107a6c61c6f3a438601fc643c3d6946c2  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-photos.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git submodule update
}

build() {
  arch-meson $pkgname build \
    -D manuals=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
