# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <plusfabi@gmail.com>
options=(debug !strip)

pkgname=gnome-initial-setup
pkgver=40.beta
pkgrel=1
pkgdesc="Simple, easy, and safe way to prepare a new system"
arch=(x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(nm-connection-editor accountsservice cheese gnome-desktop libgweather
         gnome-online-accounts libgdm libpwquality libibus libgnomekbd
         libpackagekit-glib geoclue malcontent)
makedepends=(git meson)
optdepends=('gdm: GDM Initial Setup mode'
            'gnome-getting-started-docs: GNOME Welcome Tour')
_commit=f263bf878a8fb00208cf3c024a60b2c8abbecb07  # tags/40.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-initial-setup.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # Setup system user and group
  echo 'u gnome-initial-setup - "GNOME Initial Setup" /run/gnome-initial-setup' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /run/gnome-initial-setup 0700 gnome-initial-setup gnome-initial-setup -' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
