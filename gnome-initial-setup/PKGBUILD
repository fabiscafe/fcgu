# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-initial-setup
pkgver=43.alpha
pkgrel=1
pkgdesc='Simple, easy, and safe way to prepare a new system'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/gnome-initial-setup'
license=('GPL')
options=(debug !strip)
depends=('cheese' 'gdm' 'gnome-online-accounts' 'ibus' 'malcontent' 'libgweather-4' 
         'libnm' 'libnma' 'webkit2gtk')
makedepends=('git' 'meson')
_commit=9d17896ae0cbe32a8ed49f7c4f6858f8bb839925
source=("git+https://gitlab.gnome.org/GNOME/gnome-initial-setup.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D soup2=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # Setup system user and group
  echo 'u gnome-initial-setup - "GNOME Initial Setup" /run/gnome-initial-setup' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /run/gnome-initial-setup 0700 gnome-initial-setup gnome-initial-setup -' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
