# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=gnome-session
pkgver=42.0
pkgrel=0.1
pkgdesc="The GNOME Session Handler"
url="https://gitlab.gnome.org/GNOME/gnome-session"
arch=(x86_64)
license=(GPL)
depends=(dconf gnome-desktop json-glib libgl libsm libxtst)
makedepends=(mesa gtk-doc xtrans git python xmlto docbook-xsl meson)
groups=(gnome)
_commit=7040e3e595040698fc67ddffaa5be499958237ff  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-session.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
