# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup3
pkgver=3.1.1
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64)
license=(LGPL)
depends=(glib2 glib-networking sqlite krb5 libpsl brotli libnghttp2
         libsysprof-capture)
makedepends=(gi-docgen gobject-introspection python vala git meson samba
             python-quart)
checkdepends=(apache php-apache)
optdepends=('samba: Windows Domain SSO')
_commit=ec9a66fb419b2d13872c9d0f2eb87e63cf9f433f  # tags/3.1.1^0
source=("git+https://gitlab.gnome.org/GNOME/libsoup.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libsoup
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libsoup
}

build() {
  arch-meson libsoup build \
    -D autobahn=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libgssapi_krb5.so libpsl.so
            libbrotlidec.so)
  provides+=(libsoup-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
