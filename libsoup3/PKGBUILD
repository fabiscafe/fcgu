# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug !strip)

pkgname=libsoup3
pkgver=2.99.9
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64)
license=(LGPL)
depends=("glib2>=2.69.0" libxml2 glib-networking sqlite krb5 libpsl brotli)
makedepends=(gobject-introspection python python-quart vala git gtk-doc meson samba)
checkdepends=(apache php-apache)
optdepends=('samba: Windows Domain SSO'
	    'libnghttp2')
provides=(libsoup-3.0.so libsoup-gnome-3.0.so)
_commit=dc9b33385e8766a3d1f7012dec0144f32c87d26f  # tags/2.99.9^0
source=("git+https://gitlab.gnome.org/GNOME/libsoup.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libsoup
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libsoup
}

build() {
  arch-meson libsoup build \
    -D gtk_doc=true \
    -D sysprof=disabled \
    -D autobahn=disabled
  meson compile -C build
}

check() {
  # SSL test flaky
  # https://gitlab.gnome.org/GNOME/libsoup/-/issues/188
  meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
