# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=44rc
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
depends=(gnome-shell)
makedepends=(
  git
  meson
  sassc
)
optdepends=('gnome-menus: applications menu extension')
groups=(gnome)
_commit=e0d5ede296019755f061ef6357ae9084317c75e7  # tags/44.rc^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-sass.git"
)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url data/gnome-shell-sass "$srcdir/gnome-shell-sass"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
