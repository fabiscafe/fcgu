# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=gnome-shell-extensions
pkgver=42.alpha
pkgrel=2
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
makedepends=(meson git sassc)
optdepends=('gnome-menus: applications menu extension')
groups=(gnome)
_commit=fac3d8b8c4800236362651a0c1f5abb955a64b7d  # tags/42.alpha^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gnome-shell-sass.git"
        "remove_incorrect_args_i18n.merge.patch::https://gitlab.gnome.org/GNOME/gnome-shell-extensions/-/merge_requests/210.patch")
sha256sums=('SKIP'
            'SKIP'
            '379ac11dd532d704c28eee72884bcf75fffad998816ab4ac95ff572787611d40')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url data/gnome-shell-sass "$srcdir/gnome-shell-sass"
  git submodule update
  
  # https://gitlab.gnome.org/GNOME/gnome-shell-extensions/-/merge_requests/210
  git apply -3 ../remove_incorrect_args_i18n.merge.patch
}

build() {
  arch-meson $pkgname build -D extension_set=all -D classic_mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends=(gnome-shell)

  meson install -C build --destdir "$pkgdir"
}
