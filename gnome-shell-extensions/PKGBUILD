# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=gnome-shell-extensions
pkgver=42.beta.0+r19+ga8b7386
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
depends=(gnome-shell)
makedepends=(meson git sassc)
optdepends=('gnome-menus: applications menu extension')
groups=(gnome)
_commit=a8b73861a67edea5991ad97f1b349e867ac63405  # branch:main^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gnome-shell-sass.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/beta/beta.0/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url data/gnome-shell-sass "$srcdir/gnome-shell-sass"
  git submodule update
}

build() {
  arch-meson $pkgname build \
    -D extension_set=all \
    -D classic_mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
