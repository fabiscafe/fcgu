# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-shell-extensions
pkgver=40.rc
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
makedepends=(meson git sassc js78)
optdepends=('gnome-menus: applications menu extension')
groups=(gnome)
_commit=50b6bd188458f26bfa4ec076ae9f49015b682905  # tags/40.rc^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gnome-shell-sass.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url data/gnome-shell-sass "$srcdir/gnome-shell-sass"
  git submodule update
}

build() {
  arch-meson $pkgname build -D extension_set=all -D classic_mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends=(gnome-shell)

  DESTDIR="$pkgdir" meson install -C build
}
