# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=libshumate
pkgver=1.0.0beta
pkgrel=1
pkgdesc="GTK toolkit providing widgets for embedded maps"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/libshumate"
license=(LGPL)
depends=(gtk4 libsoup3 sqlite)
makedepends=(git gi-docgen gobject-introspection gtk-doc meson vala)
checkdepends=(xorg-server-xvfb)
options=(debug)
_commit=290359fd6af168b3554c24298ba344eb88a2ed47  # tags/1.0.0.beta^0
source=("git+https://gitlab.gnome.org/GNOME/libshumate.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/([a-z])\./\1/;s/\.([a-z])/\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
