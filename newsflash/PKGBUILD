# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Felix Bühler <account at buehler dot rocks>

pkgname=newsflash
_pkgname=news_flash_gtk
pkgver=2.0.1
pkgrel=0.1
pkgdesc='Desktop application designed to complement an already existing web-based RSS reader account'
arch=('x86_64')
url='https://gitlab.com/news-flash/news_flash_gtk'
license=('GPL3')
depends=('libadwaita' 'webkit2gtk-5.0')
makedepends=('appstream-glib' 'meson' 'rust')
source=("https://gitlab.com/news-flash/$_pkgname/-/archive/v.$pkgver/$_pkgname-v.$pkgver.tar.gz")
sha256sums=('8dca3d9528d9f6c3f24af1fb2f93156a73765d2890c97aca2b76e6559434b747')

build() {
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"
  export RUSTC_BOOTSTRAP=1
	
  arch-meson $_pkgname-v.$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s com.gitlab.newsflash "$pkgdir/usr/bin/$pkgname"
}
