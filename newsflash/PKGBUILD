# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Felix Bühler <account at buehler dot rocks>
options=(debug !strip)

pkgname=newsflash
_pkgname=news_flash_gtk
pkgver=1.5.1
pkgrel=0.1
pkgdesc='Desktop application designed to complement an already existing web-based RSS reader account'
arch=('x86_64')
url='https://gitlab.com/news-flash/news_flash_gtk'
license=('GPL3')
depends=('libhandy' 'webkit2gtk')
makedepends=('appstream-glib' 'git' 'meson' 'rust')
_commit=5b5e530e1f67f491272c33095679c3b02fe50ba3 # tags/1.5.1^0
source=("git+https://gitlab.com/news-flash/news_flash_gtk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"
  export MERCURY_PARSER_USER="newsflash"
  export MERCURY_PARSER_KEY="R4qcKEHpr9RTq6YuRAPkm9kMhjp4GuxiWG44VDk3Na4ZsN7F"
  export RUSTC_BOOTSTRAP=1

  arch-meson "${_pkgname}" build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir="${pkgdir}"

  # https://gitlab.com/news-flash/news_flash_gtk/-/issues/296
  install -dm755 "${pkgdir}/usr/share/com.gitlab.newsflash"
  mv "${pkgdir}/build/newsflash/src/news_flash_gtk/data/resources/gresource_bundles/symbolic_icons.gresource" \
     "${pkgdir}/build/newsflash/src/news_flash_gtk/data/resources/gresource_bundles/ui_templates.gresource" \
     -t "${pkgdir}/usr/share/com.gitlab.newsflash/"
  rm -r "${pkgdir}/build"
}
