# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-text-editor
pkgver=42.alpha2
pkgrel=1
pkgdesc="A simple text editor"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(x86_64)
license=(GPL)
depends=(enchant gtksourceview5 glib2 libadwaita)
makedepends=(appstream-glib git meson itstool)
_commit=77817966266c006a5c3cc31b3fd6542946c8eb40  # tags/42.alpha2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
