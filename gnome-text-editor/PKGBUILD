# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-text-editor
pkgver=43.alpha0
pkgrel=1
pkgdesc="A simple text editor"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(x86_64)
license=(GPL)
options=(debug)
depends=(enchant gtksourceview5 "glib2>=2.73.0" libadwaita)
makedepends=(appstream-glib git meson itstool)
_commit=fa2fbc3fef229e1a020902cfbd9d3caba84ab2f6  # tag/43.alpha0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D bugreport_url="https://gitlab.com/fabiscafe/gnome-unstable"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
