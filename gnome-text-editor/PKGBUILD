# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-text-editor
pkgver=42.0
pkgrel=0.1
pkgdesc="A simple text editor"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(x86_64)
license=(GPL)
depends=(enchant gtksourceview5 glib2 libadwaita)
makedepends=(appstream-glib git meson itstool)
_commit=dc8bc74d0a47ffdf0d02fdb8b135ce6028c6c505  # tag/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  # appdata: fix appstream validation
  git cherry-pick -n 985c8dd411a27ad067fe9ac8951d644bad57324a
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
