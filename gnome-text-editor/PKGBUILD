# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-text-editor
pkgver=41.beta1
pkgrel=1
pkgdesc="A simple text editor"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(x86_64)
license=(GPL)
depends=(enchant gtksourceview5 "glib2>=2.69.0" libadwaita)
makedepends=(appstream-glib git meson)
checkdepends=(xorg-server-xvfb)
_commit=3508525d9285143d24c98ee27475e2d7809fbc59  # tags/41.beta1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir="${pkgdir}"
}
