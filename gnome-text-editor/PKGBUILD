# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-text-editor
pkgver=43.0
pkgrel=0.1
pkgdesc="A simple text editor"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(x86_64)
license=(GPL)
options=(debug)
depends=(enchant editorconfig-core-c gtksourceview5 "glib2>=2.74.0" libadwaita)
makedepends=(appstream-glib git meson itstool)
_commit=1ffed5916cf03d1673ea833dddc88543f9fc80e3 # tag/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D bugreport_url="https://codeberg.org/fabiscafe/gnome-unstable/issues"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
