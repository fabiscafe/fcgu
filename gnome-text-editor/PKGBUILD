# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-text-editor
pkgver=42.2
pkgrel=0.1
pkgdesc="A simple text editor"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(x86_64)
license=(GPL)
options=(debug !strip)
depends=(enchant gtksourceview5 glib2 libadwaita)
makedepends=(appstream-glib git meson itstool)
_commit=be4a2b2678f8f0f8f6728be8d827b4cf91f2545d  # tag/42.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-text-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
