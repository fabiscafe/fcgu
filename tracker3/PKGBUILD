# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
options=(debug)

pkgname=tracker3
pkgver=3.3.0.beta.0+8+g5577d18ba
pkgrel=1
pkgdesc="Desktop-neutral user information store, search tool and indexer"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(sqlite icu glib2 libffi util-linux libstemmer libseccomp libsoup libsoup3
         json-glib)
makedepends=(asciidoc gobject-introspection hotdoc vala git gtk-doc bash-completion meson systemd)
checkdepends=(python-gobject python-dbus python-tappy)
provides=(libtracker-sparql-3.0.so)
groups=(gnome)
_commit=5577d18ba71357d2d68704d2a8d602f0bb014006  # master^0
source=("git+https://gitlab.gnome.org/GNOME/tracker.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd tracker
  git describe --tags | sed 's/beta/beta.0/;s/-/+/g'
}

prepare() {
  cd tracker
}

build() {
  arch-meson tracker build \
    -D tests_tap_protocol=true
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"
}
