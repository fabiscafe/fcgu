# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=gnome-maps
pkgver=42.alpha
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(geocode-glib geoclue gjs libchamplain libgee folks gfbgraph libhandy)
makedepends=(gobject-introspection git meson yelp-tools)
groups=(gnome)
_commit=e7f845852150067027fbe46e6d09e05ac72ee6eb  # tags/v42.alpha^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git#commit=$_commit"
        "remove_incorrect_args_i18n.merge.patch")
sha256sums=('SKIP'
            '4d4cc0cf020dbeebab48b64d17e527a884604c24fc0e7a2f18b2cdaa0e3b7bb7')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  
  # https://gitlab.gnome.org/GNOME/gnome-maps/-/issues/418
  git apply -3 ../remove_incorrect_args_i18n.merge.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
