# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gcr-4
pkgver=4.0.0
pkgrel=0.1
pkgdesc="A library for bits of crypto UI and parsing"
url="https://gitlab.gnome.org/GNOME/gcr"
arch=(x86_64)
license=(GPL2)
depends=(gtk3 gtk4 libgcrypt p11-kit openssh libsecret)
makedepends=(gi-docgen gobject-introspection vala libxslt git gtk-doc meson)
provides=(libgck-2.so libgcr-4.so)
options=(debug)
_commit=dc33aca73a142ef2a4d5c4ed75161bafccd15d3e  # tags/4.0.0^0
backup=(etc/security/limits.d/10-gcr-4.conf)
install=gcr.install
source=("git+https://gitlab.gnome.org/GNOME/gcr.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gcr
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gcr
}

build() {
# ssh_agent conflicts with gcr.
  arch-meson gcr build \
    -D ssh_agent=false
  meson compile -C build
}

check() {
  # Secure memory tests fail
  dbus-run-session meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"

  # gcr wants to lock some memory to prevent swapping out private keys
  # https://bugs.archlinux.org/task/32616
  # https://bugzilla.gnome.org/show_bug.cgi?id=688161
  install -Dm644 /dev/stdin "$pkgdir/etc/security/limits.d/10-gcr-4.conf" <<END
@users - memlock 1024
END
}

# vim:set ts=2 sw=2 et:
