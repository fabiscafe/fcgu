# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=libgnome-games-support-gtk4
pkgver=2.0.beta.2
pkgrel=1
pkgdesc="Code shared between GNOME games"
url="https://gitlab.gnome.org/GNOME/libgnome-games-support"
arch=(x86_64)
license=(GPL3 LGPL3)
depends=(gtk4 libgee)
makedepends=(git meson vala)
provides=("libgnome-games-support-${pkgver%%.*}.so")
_commit=34d22fcecd8846f3055e2adb182fa47274fd5fbc  # tags/2.0.beta.2^0
source=("git+https://gitlab.gnome.org/GNOME/libgnome-games-support.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname/-gtk4//}
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ${pkgname/-gtk4//}
}

build() {
  arch-meson ${pkgname/-gtk4//} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
