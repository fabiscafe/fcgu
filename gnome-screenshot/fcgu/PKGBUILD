# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-screenshot
pkgver=40.rc
pkgrel=1
pkgdesc="Take pictures of your screen"
url="https://gitlab.gnome.org/GNOME/gnome-screenshot"
arch=(x86_64)
license=(GPL2)
depends=(gtk3 libcanberra libhandy)
makedepends=(git meson appstream-glib)
groups=(gnome)
_commit=1526c248c31888e13a4ca410811f373899256258  # tags/40.rc^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-screenshot.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
