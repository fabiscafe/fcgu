# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug)

pkgname=clapper
pkgver=0.5.1
pkgrel=2
pkgdesc="GNOME media player built using GJS with GTK4 toolkit and powered by GStreamer with OpenGL rendering."
url="https://github.com/Rafostar/clapper"
arch=(x86_64)
license=(GPL3)
depends=(gjs gst-plugins-bad gst-plugins-base gst-plugins-good
         hicolor-icon-theme libadwaita)
optdepends=(
  "gst-libav: Extra media codecs"
  "gst-plugins-ugly: Extra media codecs"
  "gstreamer-vaapi: Intel/AMD video acceleration"
  "gst-plugin-va: Intel/AMD video acceleration (gst-plugins-bad codec support)"
  )
makedepends=(appstream-glib git gobject-introspection meson)
_commit=e5723cd25a6cd0aa0f2c965ef3300bfa19e82278  # tags/0.5.1^0
source=("git+https://github.com/Rafostar/clapper.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson ${pkgname} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
