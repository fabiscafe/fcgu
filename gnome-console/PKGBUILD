# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=43.0
pkgrel=0.2
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/console"
license=('GPL3')
options=(debug)
depends=(libgtop libadwaita vte3)
makedepends=(appstream-glib git meson)
_commit=cffddb1a7bbf8ad1b1616cc026909411e218db1a  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/console.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd console
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd console
}

build() {
  arch-meson console build \
    -D tests=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
