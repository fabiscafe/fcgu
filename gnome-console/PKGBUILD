# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=43.rc
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/console"
license=('GPL3')
options=(debug)
depends=(libgtop libadwaita vte3)
makedepends=(appstream-glib git meson)
_commit=8cd9e021a92435aaf6ac3a6b681b6e5c39bbd68c  # tags/43.rc^0
source=("git+https://gitlab.gnome.org/GNOME/console.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd console
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd console
}

build() {
  arch-meson console build \
    -D tests=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
