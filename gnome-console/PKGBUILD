# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=44beta
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
url="https://gitlab.gnome.org/GNOME/console"
arch=(x86_64)
license=(GPL3)
depends=(
  libgtop
  libadwaita
  vte4
)
makedepends=(
  appstream-glib
  git
  meson
)
groups=(gnome)
_commit=04e74835a9ab48b9abac2c86f2458e20061a59da # tags/44.beta^0
source=("$pkgname::git+https://gitlab.gnome.org/GNOME/console.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D tests=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
