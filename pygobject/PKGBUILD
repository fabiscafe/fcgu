# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(python-gobject)
pkgver=3.44.0
pkgrel=0.1
pkgdesc="Python Bindings for GLib/GObject/GIO/GTK+"
url="https://wiki.gnome.org/Projects/PyGObject"
arch=(x86_64)
license=(LGPL)
depends=(
  gobject-introspection-runtime
  gtk3
  python
)
makedepends=(
  git
  gobject-introspection
  meson
  python-cairo
)
checkdepends=(
  python-pytest
  xorg-server-xvfb
)
optdepends=('cairo: Cairo bindings')
provides=("pygobject-devel=$pkgver")
conflicts=(pygobject-devel)
replaces=('pygobject-devel<=3.36.1-1')
_commit=894f8b1dd3246bad6edb75aa86f61f6d83531a33  # tags/3.44.0^0
source=("git+https://gitlab.gnome.org/GNOME/pygobject.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pygobject
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pygobject
}

build() {
  arch-meson pygobject build
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs ||:
}

package_python-gobject() {
  meson install -C build --destdir "$pkgdir"
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim:set sw=2 et:
