# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=d-spy
pkgver=1.4.0
pkgrel=1.2
pkgdesc="D-Bus debugger for GNOME"
url="https://gitlab.gnome.org/GNOME/d-spy"
arch=(x86_64)
license=(GPL3 LGPL3)
depends=(
  gtk4
  libadwaita
)
makedepends=(
  git
  meson
)
provides=(libdspy-1.so)
conflicts=(libdspy)
replaces=(libdspy)
_commit=687dc766770a0938e72fd747308f13a460b5a76f  # tags/1.4.0^0
source=("git+https://gitlab.gnome.org/GNOME/d-spy.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd d-spy
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson d-spy build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:

