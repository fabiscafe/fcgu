# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dconf-editor
pkgver=43.beta
pkgrel=1
pkgdesc="dconf Editor"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(GPL3)
groups=(gnome-extra)
depends=(dconf libhandy)
makedepends=(vala meson git)
options=(debug)
_commit=9291efd4b18bcd3bef31fc997efe99a7730080f4  # tags/43.beta^0
source=("git+https://gitlab.gnome.org/GNOME/dconf-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
