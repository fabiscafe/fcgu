# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
options=(debug)

pkgname=gnome-bluetooth-gtk4
pkgver=42.beta.0
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem (GTK4 edition)"
url="https://wiki.gnome.org/Projects/GnomeBluetooth"
arch=(x86_64)
license=(GPL LGPL)
depends=(gtk4 bluez gsound libadwaita libnotify pulseaudio-bluetooth systemd upower)
makedepends=(docbook-xsl git gobject-introspection gtk-doc meson)
checkdepends=(python-dbusmock xorg-server-xvfb)
provides=(libgnome-bluetooth-3.0.so)
_commit=1fe741e71cd6ac6f70f2e40a7a00f4c4a0e85f15  # tags/42.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-bluetooth.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gnome-bluetooth
  git describe --tags | sed 's/-/+/g;s/beta/beta.0/'
}

prepare() {
  cd gnome-bluetooth
}

build() {
  arch-meson gnome-bluetooth build \
    -D gtk_doc=true \
    -D icon_update=false \
    -D sendto=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs ||:
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
