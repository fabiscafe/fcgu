# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Fabian Bornschein <plusfabi@gmail.com>
options=(debug !strip)

pkgname=librsvg
pkgver=2.51.0
pkgrel=1
epoch=2
pkgdesc="SVG rendering library"
url="https://wiki.gnome.org/Projects/LibRsvg"
arch=(x86_64)
license=(LGPL)
depends=(gdk-pixbuf2 pango)
makedepends=(gobject-introspection vala gtk-doc git rust)
provides=(librsvg-${pkgver%%.*}.so)
_commit=02f4d0e7ff25ab91153e3b9e4e27c25b50679377 # tags/2.51.0^0
source=("git+https://gitlab.gnome.org/GNOME/librsvg.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd librsvg
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd librsvg

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd librsvg
  ./configure --prefix=/usr --disable-static --enable-vala \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

check() {
  cd librsvg
  # Test suite is very dependent on the versions of
  # Cairo, Pango, FreeType and HarfBuzz
  make check || :
}

package() {
  cd librsvg
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
