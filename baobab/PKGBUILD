# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=baobab
pkgver=44.0
pkgrel=0.1
pkgdesc="A graphical directory tree analyzer"
url="https://wiki.gnome.org/Apps/DiskUsageAnalyzer"
arch=(x86_64)
license=(GPL2)
depends=(
  dconf
  gsettings-desktop-schemas
  gtk4
  libadwaita
  librsvg
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome)
_commit=13681aee6a35c8d4f98fa7a92736ffb6dc757e0f  # tags/44.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/baobab.git#commit=$_commit"
  no-mimetype.diff
)
sha256sums=('SKIP'
            '958a85fe3a2001d16ebcf71b51888f260508af87224a0de3410a6bbe685e59d0')

pkgver() {
  cd baobab
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd baobab

  # https://bugs.archlinux.org/task/31861
  git apply -3 ../no-mimetype.diff
}

build() {
  arch-meson baobab build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
