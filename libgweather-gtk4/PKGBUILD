# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=libgweather-gtk4
pkgver=3.91.0+r38+g4ef57357
pkgrel=1
pkgdesc="Location and timezone database and weather-lookup library"
url="https://wiki.gnome.org/Projects/LibGWeather"
arch=(x86_64)
license=(LGPL)
conflicts=(libgweather4)
replaces=(libgweather4)
depends=(geocode-glib libsoup gtk4 dconf)
makedepends=(gobject-introspection gi-docgen git meson python-gobject python-pylint vala)
provides=(libgweather-4.so)
_commit=4ef5735745a7e7d7b97ee5256f4a225ea87df0cd  # branch:main^0
source=("git+https://gitlab.gnome.org/GNOME/libgweather.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libgweather
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libgweather
}

build() {
  arch-meson libgweather build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
