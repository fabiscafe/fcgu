# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=libgweather-gtk4
pkgver=4.0.0
pkgrel=0.1
pkgdesc="Location and timezone database and weather-lookup library"
url="https://wiki.gnome.org/Projects/LibGWeather"
arch=(x86_64)
license=(LGPL)
conflicts=(libgweather4)
replaces=(libgweather4)
depends=(geocode-glib libsoup gtk4 dconf)
makedepends=(clang gobject-introspection gi-docgen git meson python-gobject python-pylint vala)
provides=(libgweather-4.so)
_commit=6ac9872a5a06863f6c84f6ac836cede78b401691  # tag/4.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/libgweather.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libgweather
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libgweather
}

build() {
  arch-meson libgweather build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
