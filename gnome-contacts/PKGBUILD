# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
options=(debug)

pkgname=gnome-contacts
pkgver=42.beta.0
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
url="https://wiki.gnome.org/Apps/Contacts"
arch=(x86_64)
license=(GPL2)
depends=(dconf folks gtk4 gnome-desktop gnome-online-accounts libadwaita libgee libportal)
makedepends=(vala gobject-introspection git meson appstream-glib)
groups=(gnome)
# https://gitlab.gnome.org/GNOME/gnome-contacts/-/issues/222
conflicts=(evolution-ews)
_commit=8c128d92492737f07f62a84e332fbdb68aba4e6d  # tag/42.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g;s/beta/beta.0/'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
