# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug !strip)

pkgname=gedit
pkgver=41.alpha+r62+g64e14e13a
pkgrel=1
pkgdesc="GNOME Text Editor"
url="https://wiki.gnome.org/Apps/Gedit"
arch=(x86_64)
license=(GPL)
depends=(gtksourceview4 gsettings-desktop-schemas libpeas gspell python-gobject
         dconf)
makedepends=(yelp-tools vala gobject-introspection git gtk-doc meson
             appstream-glib desktop-file-utils libxml2)
optdepends=('gedit-plugins: Additional features')
conflicts=('gedit-code-assistance<=3.16.0+4+gd19b879-1')
groups=(gnome)
_commit=64e14e13a5d4a4b43c2a3ad2a521f8447bc560e0  # master^0
source=("git+https://gitlab.gnome.org/GNOME/gedit.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git"
        "remove_incorrect_args_i18n.merge.patch::https://gitlab.gnome.org/GNOME/gedit/-/merge_requests/124.patch")
sha256sums=('SKIP'
            'SKIP'
            '4ecfe56b4c2f784b7d3a3291a48596981eb10db43e64a2eb478c86b8a4ab4818')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git submodule update

  # https://gitlab.gnome.org/GNOME/gedit/-/merge_requests/124
  git apply -3 ../remove_incorrect_args_i18n.merge.patch
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D require_all_tests=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
