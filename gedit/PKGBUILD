# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=43.0
pkgrel=0.1
pkgdesc="GNOME Text Editor"
url="https://wiki.gnome.org/Apps/Gedit"
arch=(x86_64)
license=(GPL)
depends=(amtk gtksourceview4 gsettings-desktop-schemas libpeas gspell python-gobject tepl)
makedepends=(yelp-tools vala gobject-introspection git gtk-doc meson
             appstream-glib desktop-file-utils libxml2)
optdepends=('gedit-plugins: Additional features')
conflicts=('gedit-code-assistance<=3.16.0+4+gd19b879-1')
groups=(gnome)
options=(debug)
_commit=385d0327aa18bfdb0145238ef8bdde3d6be0e1b6  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/gedit.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git"
        "0001_positional-arg.patch::https://gitlab.gnome.org/GNOME/gedit/-/merge_requests/124.diff")
sha256sums=('SKIP'
            'SKIP'
            'd46a4c791e9f4e0b33b29a703bb5f6e798050d04491505d08c2a395522f53de8')

pkgver() {
  cd gedit
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gedit

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update

  # https://gitlab.gnome.org/GNOME/gedit/-/issues/531
  git apply -3 ../0001_positional-arg.patch
}

build() {
  arch-meson gedit build \
    -D gtk_doc=true \
    -D require_all_tests=true
  meson compile -C build
}

check() {
  # https://gitlab.gnome.org/GNOME/gedit/-/issues/532
  meson test -C build --print-errorlogs ||:
}

package() {
  meson install -C build --destdir "$pkgdir"
}
