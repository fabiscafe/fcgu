# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
options=(debug)

pkgname=gnome-bluetooth
pkgver=42.rc.0
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
url="https://wiki.gnome.org/Projects/GnomeBluetooth"
arch=(x86_64)
license=(GPL LGPL)
depends=(bluez gsound libadwaita libnotify systemd upower)
makedepends=(docbook-xsl git gobject-introspection gtk-doc meson)
checkdepends=(python-dbusmock xorg-server-xvfb)
provides=(libgnome-bluetooth-3.0.so gnome-bluetooth-gtk4)
conflicts=(gnome-bluetooth-gtk4)
replaces=(gnome-bluetooth-gtk4)
_commit=f6125d5a07a392572b04cdfac67c7e874272907f  # tag/42.rc^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-bluetooth.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gnome-bluetooth
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g;s/rc/rc.0/'
}

prepare() {
  cd gnome-bluetooth
}

build() {
  arch-meson gnome-bluetooth build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs ||:
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
