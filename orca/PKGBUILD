# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=44rc
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://wiki.gnome.org/Projects/Orca"
arch=(any)
license=(LGPL)
depends=(
  gtk3
  at-spi2-core
  python-atspi
  python-dbus
  python-xdg
  speech-dispatcher
  python-setproctitle
  liblouis
  brltty
  xorg-xmodmap
  gsettings-desktop-schemas
  gst-plugins-base # playbin, audiotestsrc, basic decoders
  gst-plugins-good # pulsesink, more decoders
)
makedepends=(
  git
  itstool
  yelp-tools
)
groups=(gnome)
_commit=4ce8ebdab20965533a9eec30225672d57161e764  # tags/ORCA_44_RC^0
source=("git+https://gitlab.gnome.org/GNOME/orca.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd orca
  git describe --tags \
    | awk '{print tolower($0)}' \
    | sed -r 's/orca_//;s/_/\./g;s/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
} 

prepare() {
  cd orca

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd orca
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd orca
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
