# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=43.beta
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://wiki.gnome.org/Projects/Orca"
arch=(any)
license=(LGPL)
depends=(gtk3 at-spi2-core python-atspi python-dbus python-xdg speech-dispatcher liblouis brltty
         xorg-xmodmap gsettings-desktop-schemas
         gst-plugins-base # playbin, audiotestsrc, basic decoders
         gst-plugins-good # pulsesink, more decoders
)
makedepends=(itstool intltool git yelp-tools)
options=(debug)
groups=(gnome)
_commit=755f2a16243d5782bd8f66ebb04a523d244994dd  # tags/ORCA_43_BETA^0
source=("git+https://gitlab.gnome.org/GNOME/orca.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/ORCA_//;s/[A-Z]/\L&/g;s/_/\./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 et:
