# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Bruno Pagani <archange@archlinux.org>
options=(debug)

pkgname=gnome-firmware
pkgver=42.0
pkgrel=0.1
pkgdesc="Manage firmware on devices supported by fwupd"
arch=(x86_64)
url="https://gitlab.gnome.org/hughsie/gnome-firmware-updater"
license=(GPL2)
depends=(fwupd libadwaita libsoup3 systemd)
makedepends=(git help2man meson)
_commit=a7abd07f449574ea152ea81914ebd4a2e444df32  # tags/42.0^0
source=("git+https://gitlab.gnome.org/hughsie/gnome-firmware-updater.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}-updater
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ${pkgname}-updater
}

build() {
  arch-meson ${pkgname}-updater build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
