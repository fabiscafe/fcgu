# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-calls
pkgver=43.alpha2
pkgrel=1
pkgdesc="A Phone dialer and call handler."
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/calls"
license=(GPL3)
replaces=("calls")
conflicts=("calls")
provides=("calls")
depends=(callaudiod feedbackd folks gom
	 libhandy libpeas modemmanager sofia-sip)
makedepends=(appstream git gobject-introspection meson python-docutils vala)
checkdepends=(xorg-server-xvfb)
_commit=3d7c671034e01756b685d7b3048b1da01d4307bf  # tags/v43.alpha.2^0
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit"
"git+https://gitlab.gnome.org/World/Phosh/libcall-ui.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd calls
  git describe --tags | sed 's/43_/43./;s/^v//;s/alpha./alpha/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd calls
  
  git submodule init
  git submodule set-url subprojects/libcall-ui "$srcdir/libcall-ui"
  git submodule update
}

build() {
  arch-meson calls build \
    -D gtk_doc=true \
    -D tests=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
