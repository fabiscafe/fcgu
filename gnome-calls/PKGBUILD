# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=gnome-calls
pkgver=41.1
pkgrel=0.2
pkgdesc="A Phone dialer and call handler."
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/calls"
license=(GPL3)
replaces=("calls")
conflicts=("calls")
provides=("calls")
depends=(callaudiod feedbackd folks gom
	 libhandy libpeas modemmanager sofia-sip)
makedepends=(git gobject-introspection meson vala)
checkdepends=(xorg-server-xvfb)
_commit=7b45de69df5a5312f702d79a23ae5e87979857ef  # tags/41.1^0
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname//gnome-/}
  git describe --tags | sed 's/-/+/g;s/^v//g;s/_/./g'
}

build() {
  arch-meson ${pkgname//gnome-/} build \
    -D gtk_doc=true \
    -D tests=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
