# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-calls
pkgver=43.2
pkgrel=0.1
pkgdesc="A Phone dialer and call handler."
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/calls"
license=(GPL3)
replaces=("calls")
conflicts=("calls")
provides=("calls")
options=(debug !strip)
depends=(callaudiod feedbackd folks gom
	 libhandy libpeas modemmanager sofia-sip)
makedepends=(appstream git gobject-introspection meson python-docutils vala)
checkdepends=(xorg-server-xvfb)
_commit=5fa511c556145e0bdaa6f3eb79a9092965f91f3b  # tags/v43.2^0
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit"
"git+https://gitlab.gnome.org/World/Phosh/libcall-ui.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd calls
  git describe --tags | sed -r 's/^v//;s/_/./;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd calls
  
  git submodule init
  git submodule set-url subprojects/libcall-ui "$srcdir/libcall-ui"
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson calls build \
    -D gtk_doc=true \
    -D tests=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
