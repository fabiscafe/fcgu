# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
options=(debug !strip)

pkgname=eog
pkgver=40.rc
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 exempi libpeas librsvg dconf libportal)
makedepends=(gobject-introspection gtk-doc git meson yelp-tools)
optdepends=('eog-plugins: Additional features')
groups=(gnome)
_commit=3e37d373eaafc17af58a1356bd6d94437afe7d8e  # tags/40.rc^0
source=("git+https://gitlab.gnome.org/GNOME/eog.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
