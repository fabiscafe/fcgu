# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=eog
pkgver=42.rc.0
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL)
depends=(dconf exempi gnome-desktop lcms2 libexif libhandy librsvg libpeas)
makedepends=(gi-docgen git gobject-introspection itstool meson )
optdepends=('eog-plugins: Additional features')
groups=(gnome)
_commit=d7c8cc26e5d449073df1afb63d1f310a3991f063  # tag/42.rc^0
source=("git+https://gitlab.gnome.org/GNOME/eog.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/rc/rc.0/;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D libportal=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
