# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=eog
pkgname=(eog eog-docs)
pkgver=43.rc
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL)
# 43.beta advertises webp support via MimeType list, add webp-pixbuf-loader as dependency
depends=(gnome-desktop libexif lcms2 exempi libpeas librsvg libhandy webp-pixbuf-loader
         libportal libportal-gtk3)
makedepends=(gobject-introspection gi-docgen git meson yelp-tools systemd)
options=(debug)
_commit=d55f5d89499ab07dbaca4e3643c55ee6eba38ccd  # tags/43.rc^0
source=("git+https://gitlab.gnome.org/GNOME/eog.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd eog
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson eog build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_eog() {
  optdepends=('eog-plugins: Additional features')
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_eog-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
