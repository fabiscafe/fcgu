# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Fabian Bornschein <plusfabi@gmail.com>
options=(debug !strip)

pkgname=gnome-weather
pkgver=40.beta
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL)
depends=(gtk3 gjs libhandy libgweather geoclue2 gnome-desktop)
makedepends=(gobject-introspection appstream-glib git meson)
groups=(gnome)
_commit=5fa8fb17c7b58ff972ae9951017024ce1f30fa42  # tags/40.beta^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-weather.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
