# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=callaudiod
pkgver=0.1.9
pkgrel=0.1
pkgdesc="Call audio routing daemon"
arch=(x86_64)
url="https://gitlab.com/mobian1/callaudiod"
license=(GPL3)
options=(!strip)
depends=(
  alsa-lib
  glib2
  libpulse
)
makedepends=(
  git
  gtk-doc
  meson
)
_commit=50d1ac3792aac03abe0a3a88ceac77201a43bb3b  # tags/0.1.9^0
source=("git+https://gitlab.com/mobian1/callaudiod.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
