# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=callaudiod
pkgver=0.1.6
pkgrel=0.2
pkgdesc="Call audio routing daemon"
arch=(x86_64)
url="https://gitlab.com/mobian1/callaudiod"
license=(GPL3)
options=(!strip)
depends=(
  alsa-lib
  glib2
  libpulse
)
makedepends=(
  git
  gtk-doc
  meson
)
_commit=a029a5ce7cf77c052a58ee079e74102966428ab9  # tags/0.1.6^0
source=("git+https://gitlab.com/mobian1/callaudiod.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
