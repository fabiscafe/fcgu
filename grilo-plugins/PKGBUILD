# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=grilo-plugins
pkgver=0.3.15
pkgrel=1.3
epoch=1
pkgdesc="A collection of plugins for the Grilo framework"
url="https://gitlab.gnome.org/GNOME/grilo-plugins"
arch=(x86_64)
license=(LGPL)
options=(debug)
depends=(grilo lua libmediaart libgdata tracker3 gom chromaprint
         gst-plugins-bad gmime3)
makedepends=(git meson gperf yelp-tools)
checkdepends=(gst-plugins-good gst-plugins-base python-gobject tracker3-miners)
optdepends=('dleyna-server: DLNA plugin'
            'tracker3-miners: Tracker plugin')
groups=(gnome)
_commit=98cbc65e258209c92a260a7641e4b9df2c9e58fa  # tags/grilo-plugins-0.3.15^0
source=("git+https://gitlab.gnome.org/GNOME/grilo-plugins.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^grilo-plugins-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    # Tracker 1 is deprecated
    -D enable-tracker=no

    # libdmapsharing requires libsoup2 and wont work with libsoup3.
    # this prevents applications from loading, like gnome-photos
    # https://gitlab.gnome.org/GNOME/libdmapsharing/-/issues/53
    -D enable-dmap=no
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package() {
  depends+=(libgr{ilo,lnet,lpls}-0.3.so)
  meson install -C build --destdir "$pkgdir"
}
