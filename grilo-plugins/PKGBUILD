# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=grilo-plugins
pkgver=0.3.15
pkgrel=2.1
epoch=1
pkgdesc="A collection of plugins for the Grilo framework"
url="https://gitlab.gnome.org/GNOME/grilo-plugins"
arch=(x86_64)
license=(LGPL)
depends=(
  grilo
  lua
  libmediaart
  libgdata
  tracker3
  gom
  libdmapsharing-4.0.so
  chromaprint
  gst-plugins-bad
  gmime3
)
makedepends=(
  git
  meson
  gperf
  yelp-tools
)
checkdepends=(
  gst-plugins-good
  gst-plugins-base
  python-gobject
  tracker3-miners
)
optdepends=(
  'dleyna-server: DLNA plugin'
  'tracker3-miners: Tracker plugin'
)
groups=(gnome)
_commit=98cbc65e258209c92a260a7641e4b9df2c9e58fa  # tags/grilo-plugins-0.3.15^0
source=("git+https://gitlab.gnome.org/GNOME/grilo-plugins.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^grilo-plugins-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_args=(
    -D enable-tracker=no

    # Gnome-music
    ## music is not compatible with soup2 anymore. If plugins are found that
    ## still uses soup2 gnome-music will refuse to start.
    ## One downside of this change is that totem wont be able to search for
    ## subtitles and youtube. The Youtube video fetcher of totem however
    ## also seems to be broken on Arch. So no real reason to keep this enabled.
    ## It works btw on flatpak.
    -D enable-opensubtitles=no
    ## https://gitlab.gnome.org/GNOME/grilo-plugins/-/issues/82
    -D enable-youtube=no
    ## https://gitlab.gnome.org/GNOME/grilo-plugins/-/merge_requests/124
  )

  arch-meson $pkgname build "${meson_args[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs || :
}

package() {
  depends+=(libgr{ilo,lnet,lpls}-0.3.so)
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
