# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=phodav
pkgver=3.0
pkgrel=0.1
pkgdesc="WebDav server implementation using libsoup3 (RFC 4918)"
arch=('x86_64')
url="https://wiki.gnome.org/phodav"
license=('LGPL2.1')
depends=('avahi' 'libsoup3' 'systemd')
makedepends=('asciidoc' 'git' 'gtk-doc' 'meson' 'xmlto')
backup=('etc/conf.d/spice-webdavd')
_commit=d733fd853f0664ad8035b1b85604c62de0e97098  # tags/3.0^0
source=("git+https://gitlab.gnome.org/GNOME/phodav.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd phodav
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd phodav

  sed -i 's|/etc/sysconfig/spice-webdavd|/etc/conf.d/spice-webdavd|
	      s|/usr/sbin/spice-webdavd|/usr/bin/spice-webdavd|' data/spice-webdavd.service
}

build() {
	arch-meson phodav build
	meson compile -C build
}

package() {
	meson install -C build --destdir "$pkgdir"

	install -dm755 "$pkgdir/etc/conf.d/"
	echo 'SPICE_WEBDAVD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-webdavd"
}
