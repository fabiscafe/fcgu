# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tracker3-miners
pkgver=3.5.0alpha
pkgrel=2
pkgdesc="Collection of data extractors for Tracker/Nepomuk"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(
  exempi
  giflib
  gst-plugins-base-libs
  gupnp-dlna
  libcue
  libexif
  libgexiv2
  libgrss
  libgsf
  libgxps
  libiptcdata
  libnm
  libosinfo
  libseccomp
  poppler-glib
  totem-plparser
  tracker3
  upower
)
makedepends=(
  asciidoc
  git
  meson
)
checkdepends=(
  gst-libav
  gst-plugins-base
  gst-plugins-good
  python-gobject
  python-tappy
)
groups=(gnome)
_commit=145a7e1952858187e185f5b54f0946089680bf67  # tags/3.5.0.alpha^0
source=("git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd tracker-miners
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd tracker-miners
}

build() {
  arch-meson tracker-miners build -D tests_tap_protocol=true
  meson compile -C build
}

check() {
  # Intermittent test failures: https://gitlab.gnome.org/GNOME/tracker-miners/-/issues/79
  dbus-run-session meson test -C build --print-errorlogs -t 3 || :
}

package() {
  depends+=(libtracker-sparql-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
