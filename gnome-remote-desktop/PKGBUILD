# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
options=(debug)

pkgname=gnome-remote-desktop
pkgver=42.0
pkgrel=0.1
pkgdesc="GNOME Remote Desktop server"
url="https://wiki.gnome.org/Projects/Mutter/RemoteDesktop"
arch=(x86_64)
license=(GPL2)
depends=(cairo freerdp glib2 pipewire systemd libvncserver libsecret libnotify
         dconf fuse3 libxkbcommon)
makedepends=(git meson ffnvcodec-headers)
checkdepends=(xorg-server-xvfb python-dbus python-gobject mutter
              wireplumber)
groups=(gnome)
_commit=252f495a0f23ca25056cc5815587255614ffdfc7  # tag/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-remote-desktop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

_check() {
  mkdir -p -m 700 "${XDG_RUNTIME_DIR:=$PWD/runtime-dir}"
  glib-compile-schemas "${GSETTINGS_SCHEMA_DIR:=$PWD/build/src}"
  export XDG_RUNTIME_DIR GSETTINGS_SCHEMA_DIR
  
  pipewire &
  _p1=$!
  sleep 1

  wireplumber &
  _p2=$!
  sleep 1

  trap "kill $_p1 $_p2; wait" EXIT

  ## enable vnc backend
  gsettings set org.gnome.desktop.remote-desktop.vnc enable true
  meson test -C build --print-errorlogs -t 2
}

check() {
  dbus-run-session -- bash -c "$(declare -f _check); _check" |:
}

package() {
  meson install -C build --destdir "$pkgdir"
}
