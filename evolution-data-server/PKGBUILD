# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.47.3
pkgrel=3
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
depends=(
  gcr-4
  gnome-online-accounts
  krb5
  libcanberra
  libgweather-4
  libical
  libphonenumber
  nss
  sqlite
  webkitgtk-6.0
)
makedepends=(
  boost
  cmake
  git
  gobject-introspection
  gperf
  gtk-doc
  ninja
  vala
)
provides=(
  libcamel-1.2.so
  libebackend-1.2.so
  libebook-1.2.so
  libebook-contacts-1.2.so
  libecal-2.0.so
  libedata-book-1.2.so
  libedata-cal-2.0.so
  libedataserver-1.2.so
  libedataserverui-1.2.so
  libedataserverui4-1.0.so
)
license=(GPL)
_commit=68ffcb67d34cd301de65198d6f8757eac0d1a59a  # tags/3.47.3^0
source=("git+https://gitlab.gnome.org/GNOME/evolution-data-server.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBEXEC_INSTALL_DIR=/usr/lib
    -DSYSCONF_INSTALL_DIR=/etc
    -DENABLE_GTK_DOC=ON
    -DENABLE_INTROSPECTION=ON
    -DENABLE_VALA_BINDINGS=ON
    -DWITH_GWEATHER4=ON
    -DWITH_LIBDB=OFF
    -DWITH_PHONENUMBER=ON
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
