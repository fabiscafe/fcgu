# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calculator
pkgver=43.0.1
pkgrel=0.1
pkgdesc="GNOME Scientific calculator"
url="https://wiki.gnome.org/Apps/Calculator"
arch=(x86_64)
license=(GPL)
depends=(libadwaita dconf gtksourceview5 mpfr libsoup3 libmpc libgee)
makedepends=(yelp-tools vala git meson gobject-introspection)
groups=(gnome)
options=(debug)
_commit=debc596cdfaaa3cbfa4300699fcb63362c595507  # tags/43.0.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-calculator.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
