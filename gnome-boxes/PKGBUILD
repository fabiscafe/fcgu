# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=43.rc
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Boxes'
license=('LGPL')
groups=('gnome')
depends=('cdrtools' 'edk2-ovmf' 'libarchive' 'libgudev' 'libhandy'
         'libosinfo' 'libsecret' 'libvirt-glib' 'mtools' 'qemu-desktop' 'spice-gtk' 'tracker3'
         'webkit2gtk-4.1' 'libsoup3')
makedepends=('git' 'appstream-glib' 'gobject-introspection' 'itstool' 'meson' 'spice-protocol' 'vala')
options=('debug')
_commit=c64a6d994fe00c0ccad66f42185da99272a97585  # tags/43.rc^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
