# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=44rc
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Boxes'
license=('LGPL')
depends=(
  'cdrtools'
  'edk2-ovmf'
  'libarchive'
  'libgudev'
  'libhandy'
  'libosinfo'
  'libsecret'
  'libsoup3'
  'libvirt-glib'
  'mtools'
  'qemu-desktop'
  'spice-gtk'
  'tracker3'
  'webkit2gtk-4.1'
)
makedepends=(
  'appstream-glib'
  'git'
  'gobject-introspection'
  'meson'
  'spice-protocol'
  'vala'
  'yelp-tools'
)
groups=('gnome-extra')
_commit=732983c9c72e61bf92b3568a3600896b066daca8 # tags/44.rc^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
