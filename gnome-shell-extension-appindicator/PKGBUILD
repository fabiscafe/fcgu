# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Contributor: Ewout van Mansom <ewout@vanmansom.name>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gnome-shell-extension-appindicator
pkgver=42+r7+ga9ffd94
pkgrel=1
pkgdesc='AppIndicator/KStatusNotifierItem support for GNOME Shell'
url='https://github.com/ubuntu/gnome-shell-extension-appindicator'
arch=('any')
license=('GPL')
depends=('gnome-shell>=43')
makedepends=('git' 'meson' 'jq')
optdepends=(
  'libappindicator-gtk2: support GTK+2 applications'
  'libappindicator-gtk3: support GTK+3 applications'
)
_commit=a9ffd9467d5e1c3c19199272ec0eb36d286ecc3a # master
source=(
  "git+https://github.com/ubuntu/${pkgname}.git#commit=${_commit}"
  "gnome43-comp.patch::https://patch-diff.githubusercontent.com/raw/ubuntu/gnome-shell-extension-appindicator/pull/351.patch"
)
sha256sums=('SKIP'
            '37ce0e2681b385464b2681b0a0f1f4fe04c992c77083cb0d1b886c4bca09e3e7')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd "${pkgname}"
  patch --forward --strip=1 --input="${srcdir}/gnome43-comp.patch"
}

build() {
  cd "${pkgname}"
  meson --prefix=/usr build/
  ninja -C build/
}

package() {
  cd "${pkgname}"
  DESTDIR="${pkgdir}" ninja -C build/ install
  # this file is generated at pkg install time
  rm "${pkgdir}/usr/share/glib-2.0/schemas/gschemas.compiled"
}

# vim: ts=2 sw=2 et:
