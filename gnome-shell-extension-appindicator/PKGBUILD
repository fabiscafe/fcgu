# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Contributor: Ewout van Mansom <ewout@vanmansom.name>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gnome-shell-extension-appindicator
pkgver=43.0
pkgrel=0.1
pkgdesc='AppIndicator/KStatusNotifierItem support for GNOME Shell'
url='https://github.com/ubuntu/gnome-shell-extension-appindicator'
arch=('any')
license=('GPL')
depends=('gnome-shell>=43')
makedepends=('git' 'meson' 'jq')
optdepends=(
  'libappindicator-gtk2: support GTK+2 applications'
  'libappindicator-gtk3: support GTK+3 applications'
)
_commit=c49dde65b2c9e02fb7d6109e2efdd0b6aa34db2c # tags/43^0
source=("git+https://github.com/ubuntu/${pkgname}.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed -r '/\.([0-9])/!s/$/\.0/;s/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd "${pkgname}"
}

build() {
  cd "${pkgname}"
  meson --prefix=/usr build/
  ninja -C build/
}

package() {
  cd "${pkgname}"
  DESTDIR="${pkgdir}" ninja -C build/ install
  # this file is generated at pkg install time
  rm "${pkgdir}/usr/share/glib-2.0/schemas/gschemas.compiled"
}

# vim: ts=2 sw=2 et:
