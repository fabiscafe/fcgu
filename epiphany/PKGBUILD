# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
options=(debug)

pkgname=epiphany
pkgver=42.0
pkgrel=0.1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(x86_64)
license=(GPL)
depends=(gcr icu libarchive libdazzle libhandy libportal-gtk3 webkit2gtk-4.1)
makedepends=(docbook-xml git gobject-introspection lsb-release meson
             startup-notification yelp-tools)
checkdepends=(aspell hspell hunspell nuspell libvoikko xorg-server-xvfb)
groups=(gnome)
_commit=80217e512db2bd375c8b5b959660099aeeee9ed9  # tag/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/epiphany.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D soup2=disabled
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
