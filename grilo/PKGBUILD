# Maintainer: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=grilo
pkgver=0.3.14+r19+gfccc618
pkgrel=1
pkgdesc="Framework that provides access to various sources of multimedia content"
url="https://wiki.gnome.org/Projects/Grilo"
arch=(x86_64)
license=(LGPL)
options=(debug)
depends=(libxml2 libsoup3 liboauth totem-plparser)
makedepends=(gobject-introspection gtk-doc vala git meson gtk3)
optdepends=('grilo-plugins: Plugins for grilo'
            'gtk3: Test UI')
provides=(libgr{ilo,lnet,lpls}-0.3.so)
_commit=fccc61863b4e049459cfe2213edb97afb95d960a  # tags/master^0
source=("git+https://gitlab.gnome.org/GNOME/grilo.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^grilo-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
